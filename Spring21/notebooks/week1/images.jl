### A Pluto.jl notebook ###
# v0.14.5

using Markdown
using InteractiveUtils

# This Pluto notebook uses @bind for interactivity. When running this notebook outside of Pluto, the following 'mock version' of @bind gives bound variables a default value (instead of an error).
macro bind(def, element)
    quote
        local el = $(esc(element))
        global $(esc(def)) = Core.applicable(Base.get, el) ? Base.get(el) : missing
        el
    end
end

# â•”â•â•¡ e91d7926-ec6e-41e7-aba2-9dca333c8aa5
html"""
<div style="
position: absolute;
width: calc(100% - 30px);
border: 50vw solid #282936;
border-top: 500px solid #282936;
border-bottom: none;
box-sizing: content-box;
left: calc(-50vw + 15px);
top: -500px;
height: 500px;
pointer-events: none;
"></div>

<div style="
height: 500px;
width: 100%;
background: #282936;
color: #fff;
padding-top: 68px;
">
<span style="
font-family: Vollkorn, serif;
font-weight: 700;
font-feature-settings: 'lnum', 'pnum';
"> <p style="
font-size: 1.5rem;
opacity: .8;
"><em>Section 1.1</em></p>
<p style="text-align: center; font-size: 2rem;">
<em> Images as Data and Arrays </em>
</p>

<p style="
font-size: 1.5rem;
text-align: center;
opacity: .8;
"><em>Lecture Video</em></p>
<div style="display: flex; justify-content: center;">
<div  notthestyle="position: relative; right: 0; top: 0; z-index: 300;">
<iframe src="https://www.youtube.com/embed/3zTO3LEY-cM" width=400 height=250  frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe></div>
</div>
</div>

<style>
body {
overflow-x: hidden;
}
</style>"""

# â•”â•â•¡ d07fcdb0-7afc-4a25-b68a-49fd1e3405e7
PlutoUI.TableOfContents(aside=true)

# â•”â•â•¡ 9b49500c-0164-4556-a17b-7595e35c5ede
md"""
#### Intializing packages

_When running this notebook for the first time, this could take up to 15 minutes. Hang in there!_
"""

# â•”â•â•¡ 74b008f6-ed6b-11ea-291f-b3791d6d1b35
begin
	import Pkg
	Pkg.activate(mktempdir())
	Pkg.add([
		Pkg.PackageSpec(name="ImageIO", version="0.5"),
		Pkg.PackageSpec(name="ImageShow", version="0.2"),
		Pkg.PackageSpec(name="FileIO", version="1.6"),
		Pkg.PackageSpec(name="PNGFiles", version="0.3.6"),
		Pkg.PackageSpec(name="Colors", version="0.12"),
		Pkg.PackageSpec(name="ColorVectorSpace", version="0.8"),
		Pkg.PackageSpec(name="PlutoUI", version="0.7"), 
		Pkg.PackageSpec(name="HypertextLiteral", version="0.5")
	])

	using Colors, ColorVectorSpace, ImageShow, FileIO
	using PlutoUI
	using HypertextLiteral
end

# â•”â•â•¡ 71a1e08a-6abc-48d5-b177-5184dbdd76a8
filter!(LOAD_PATH) do path
	path != "@v#.#"
end;

# â•”â•â•¡ ca1b507e-6017-11eb-34e6-6b85cd189002
md"""
# Images as examples of data  all around us
Welcome to the Computational Thinking using Julia for Real-World Problems, at MIT in Spring 2021!

The aim of this course is to bring together concepts from computer science and applied math with coding in the modern **Julia language**, and to see how to apply these techniques to study interesting applications (and of course to have fun).

We would be pleased if students who have been interested in computer science now become interested in computational science and those interested in scientific applications learn computer science they may not see elsewhere.
... and for all students, we wish to share the value of 
the Julia  language as the best of both worlds.
"""

# â•”â•â•¡ e9ff96d8-6bc1-11eb-0f6a-234b9fae047e
md"""

## Alan's Essay: Are all programming languages the same? 

>Superficially, many programming languages are very similar.  "Showoffs" will compare functional programming vs imperative programming.  Others will compare compiled languages vs dynamic languages.  I will avoid such fancy terms in this little essay, preferring to provide this course's pedagogical viewpoint.
>
>Generally speaking beginning programmers should learn to create "arrays" write "for loops", "conditionals", "comparisons", express mathematical formulas, etc. So why Julia at a time when Python seems to be the language of teaching, and Java and C++ so prominent in the corporate world?
>
>As you might imagine, we believe Julia is special.   Oh you will still have the nitty gritty of when to use a bracket and a comma.  You might have strong opinions as to whether arrays should begin with 0 or 1 (joke: some say it's time to compromise and use Â½.)  Getting past these irrelevant issues,  you will begin to experience one by one what makes Julia so very special.  For starters, a language that runs fast is more fun.  We can have you try things that would just be so slow in other languages it would be boring.  We also think you will start to notice how natural Julia is, how it feels like the mathematics, and how flexible it can be.  
>
>Getting to see the true value of fancy terms like multiple dispatch, strong typing, generic programming, and composable software will take a little longer, but stick with us, and you too will see why Julia is so very special.
"""

# â•”â•â•¡ 9111db10-6bc3-11eb-38e5-cf3f58536914
md"""
## Computer Science and Computational Science Working Together
"""

# â•”â•â•¡ fb8a99ac-6bc1-11eb-0835-3146734a1c99
md"""
Applications of computer science in the real world use **data**, i.e. information that we can **measure** in some way. Data take many different forms, for example:

- Numbers that change over time (**time series**): 
  - Stock price each second / minute / day
  - Weekly number of infections
  - Earth's global average temperature

- Video:
  - The view from a window of a self-driving car
  - A hurricane monitoring station
  - Ultrasound e.g. prenatal

- Images:
  - Diseased versus healthy tissue in a medical scan
  - Pictures of your favourite dog
"""

# â•”â•â•¡ b795dcb4-6bc3-11eb-20ec-db2cc4b89bfb
md"""
#### Exercise: 
> Think of another two examples in each category. Can you think of other categories of data?
"""

# â•”â•â•¡ 8691e434-6bc4-11eb-07d1-8169158484e6
md"""
Computational science can be summed up by a simplified workflow:
"""

# â•”â•â•¡ 546db74c-6d4e-11eb-2e27-f5bed9dbd9ba
md"""
## data âŸ¶  input  âŸ¶ process âŸ¶ model âŸ¶ visualize âŸ¶ output
"""

# â•”â•â•¡ 6385d174-6d4e-11eb-093b-6f6fafb79f84
md"""
$(html"<br>")
To use any data source, we need to **input** the data of interest, for example by downloading it, reading in the resulting file, and converting it into a form that we can use in the computer. Then we need to **process** it in some way to extract information of interest. We usually want to **visualize** the results, and we may want to **output** them, for example by saving to disc or putting them on a website.

We often want to make a mathematical or computational **model** that can help us to understand and predict the behavior of the system of interest.

> In this course we aim to show how programming, computer science and applied math combine to help us with these goals.
"""

# â•”â•â•¡ 132f6596-6bc6-11eb-29f1-1b2478c929af
md"""
# Data: Images (as an example of data)
Let's start off by looking at **images** and how we can process them. 
Our goal is to process the data contained in an image in some way, which we will do by developing and coding certain **algorithms**.

Here is the the Fall 2020 version of this lecture (small variations) by 3-Blue-1-Brown (Grant Sanderson) for your reference.
"""

# â•”â•â•¡ 635a03dd-abd7-49c8-a3d2-e68c7d83cc9b
html"""
<div notthestyle="position: relative; right: 0; top: 0; z-index: 300;"><iframe src="https://www.youtube.com/embed/DGojI9xcCfg" width=400 height=250  frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe></div>
"""

# â•”â•â•¡ 9eb6efd2-6018-11eb-2db8-c3ce41d9e337
md"""


If we open an image on our computer or the web and zoom in enough, we will see that it consists of many tiny squares, or **pixels** ("picture elements"). Each pixel is a block of one single colour, and the pixels are arranged in a two-dimensional square grid. 

You probably already know that these pixels are stored in a computer numerically
perhaps in some form of RGB (red,green,blue) format.  This is the computer's represenation of the data.   

Note that an image is already an **approximation** of the real world -- it is a two-dimensional, discrete representation of a three-dimensional reality.

"""

# â•”â•â•¡ e37e4d40-6018-11eb-3e1d-093266c98507
md"""
# Input and Visualize: loading and viewing an Image (in Julia)
"""

# â•”â•â•¡ e1c9742a-6018-11eb-23ba-d974e57f78f9
md"""
Let's use Julia to load  actual images and play around with them. We can download images from the internet, your own file, or your own webcam.
"""

# â•”â•â•¡ 9b004f70-6bc9-11eb-128c-914eadfc9a0e
md"""
## Downloading an image from the internet or a local file
We can use the `Images.jl` package to load an image file in three steps.
"""

# â•”â•â•¡ 62fa19da-64c6-11eb-0038-5d40a6890cf5
md"""
Step 1: (from internet) we specify the URL (web address) to download from:
$(html"<br>")
(note that Pluto places results before commands because some people believe
output is more interesting than code.  This takes some getting used to.)
"""

# â•”â•â•¡ 34ee0954-601e-11eb-1912-97dc2937fd52
url = "https://user-images.githubusercontent.com/6933510/107239146-dcc3fd00-6a28-11eb-8c7b-41aaf6618935.png" 

# â•”â•â•¡ 9180fbcc-601e-11eb-0c22-c920dc7ee9a9
md"""
Step 2: Now we use the aptly-named `download` function to download the image file to our own computer. (Philip is Prof. Edelman's corgi.)
"""

# â•”â•â•¡ 34ffc3d8-601e-11eb-161c-6f9a07c5fd78
philip_filename = download(url) # download to a local file. The filename is returned

# â•”â•â•¡ abaaa980-601e-11eb-0f71-8ff02269b775
md"""
Step 3:
Using the `Images.jl` package (loaded at the start of this notebook; scroll up and take a look.) we can **load** the file, which automatically converts it into usable data. We'll store the result in a variable. (Remember the code is after the output.)
"""

# â•”â•â•¡ aafe76a6-601e-11eb-1ff5-01885c5238da
philip = load(philip_filename)

# â•”â•â•¡ e86ed944-ee05-11ea-3e0f-d70fc73b789c
md"_Hi there Philip_"

# â•”â•â•¡ c99d2aa8-601e-11eb-3469-497a246db17c
md"""
We see that the Pluto notebook has recognised that we created an object representing an image, and automatically displayed the resulting image of Philip, the cute Welsh Pembroke corgi and co-professor of this course.
Poor Philip will undergo quite a few transformations as we go along!
"""

# â•”â•â•¡ 11dff4ce-6bca-11eb-1056-c1345c796ed4
md"""
- Exercise : change the url.
- Exercise: download a file that is already on your own computer.
"""

# â•”â•â•¡ efef3a32-6bc9-11eb-17e9-dd2171be9c21
md"""
## Capturing an Image from your own camera
"""

# â•”â•â•¡ e94dcc62-6d4e-11eb-3d53-ff9878f0091e
md"""
Even more fun is to use your own webcam. Try pressing the enable button below. Then
press the camera to capture an image. Kind of fun to keep pressing the button as you move your hand etc.
"""

# â•”â•â•¡ d6742ea0-1106-4f3c-a5b8-a31a48d33f19
@bind webcam_data1 camera_input()

# â•”â•â•¡ 1d7375b7-7ea6-4d67-ab73-1c69d6b8b87f
myface1 = process_raw_camera_data(webcam_data1);

# â•”â•â•¡ 6224c74b-8915-4983-abf0-30e6ba04a46d
[
	myface1              myface1[   :    , end:-1:1]
	myface1[end:-1:1, :] myface1[end:-1:1, end:-1:1]
]

# â•”â•â•¡ cef1a95a-64c6-11eb-15e7-636a3621d727
md"""
## Inspecting your data
"""

# â•”â•â•¡ f26d9326-64c6-11eb-1166-5d82586422ed
md"""
### Image size
"""

# â•”â•â•¡ 6f928b30-602c-11eb-1033-71d442feff93
md"""
The first thing we might want to know is the size of the image:
"""

# â•”â•â•¡ 75c5c85a-602c-11eb-2fb1-f7e7f2c5d04b
philip_size = size(philip)

# â•”â•â•¡ 77f93eb8-602c-11eb-1f38-efa56cc93ca5
md"""
Julia returns a pair of two numbers. Comparing these with the picture of the image, we see that the first number is the height, i.e. the vertical number of pixels, and the second is the width.
"""

# â•”â•â•¡ 96b7d801-c427-4e27-ab1f-e2fd18fc24d0
philip_height = philip_size[1]

# â•”â•â•¡ f08d02af-6e38-4ace-8b11-7af4930b64ea
philip_width = philip_size[2]

# â•”â•â•¡ f9244264-64c6-11eb-23a6-cfa76f8aff6d
md"""
### Locations in an image: Indexing

Now suppose that we want to examine a piece of the image in more detail. We need some way of specifying which piece of the image we want. 

Thinking of the image as a grid of pixels, we need a way to tell the computer which pixel or group of pixels we want to refer to. 
Since the image is a two-dimensional grid, we can use two integers (whole numbers) to give the coordinates of a single pixel.  Specifying coordinates like this is called **indexing**: think of the index of a book, which tells you *on which page* an idea is discussed.

In Julia we use (square) brackets, `[` and `]` for indexing: 
"""

# â•”â•â•¡ bd22d09a-64c7-11eb-146f-67733b8be241
a_pixel = philip[200, 100]

# â•”â•â•¡ 28860d48-64c8-11eb-240f-e1232b3638df
md"""
We see that Julia knows to draw our pixel object for us a block of the relevant color.

When we index into an image like this, the first number indicates the *row* in the image, starting from the top, and the second the *column*, starting from the left. In Julia, the first row and column are numbered starting from 1, not from 0 as in some other programming languages.
"""

# â•”â•â•¡ 4ef99715-4d8d-4f9d-bf0b-8df9907a14cf


# â•”â•â•¡ a510fc33-406e-4fb5-be83-9e4b5578717c
md"""
We can also use variables as indices...
"""

# â•”â•â•¡ 94b77934-713e-11eb-18cf-c5dc5e7afc5b
row_i,col_i

# â•”â•â•¡ ff762861-b186-4eb0-9582-0ce66ca10f60
philip[row_i, col_i]

# â•”â•â•¡ 13844ebf-52c4-47e9-bda4-106a02fad9d7
md"""
...and these variables can be controlled by sliders!
"""

# â•”â•â•¡ 08d61afb-c641-4aa9-b995-2552af89f3b8
@bind row_i Slider(1:size(philip)[1], show_value=true)

# â•”â•â•¡ 6511a498-7ac9-445b-9c15-ec02d09783fe
@bind col_i Slider(1:size(philip)[2], show_value=true)

# â•”â•â•¡ c9ed950c-dcd9-4296-a431-ee0f36d5b557
md"""
### Locations in an image: Range indexing

We saw that we can use the **row number** and **column number** to index a _single pixel_ of our image. Next, we will use a **range of numbers** to index _multiple rows or columns_ at once, returning a subarray:
"""

# â•”â•â•¡ f0796032-8105-4f6d-b5ee-3647b052f2f6
philip[550:650, 1:philip_width]

# â•”â•â•¡ b9be8761-a9c9-49eb-ba1b-527d12097362
md"""
Here, we use `a:b` to mean "_all numbers between `a` and `b`_". For example:

"""

# â•”â•â•¡ d515286b-4ad4-449b-8967-06b9b4c87684
collect(1:10)

# â•”â•â•¡ eef8fbc8-8887-4628-8ba8-114575d6b91f
md"""

You can also use a `:` without start and end to mean "_every index_"
"""

# â•”â•â•¡ 4e6a31d6-1ef8-4a69-b346-ad58cfc4d8a5
philip[550:650, :]

# â•”â•â•¡ e11f0e47-02d9-48a6-9b1a-e313c18db129
md"""
Let's get a single row of pixels:
"""

# â•”â•â•¡ 9e447eab-14b6-45d8-83ab-1f7f1f1c70d2
philip[550, :]

# â•”â•â•¡ c926435c-c648-419c-9951-ac8a1d4f3b92
philip_head = philip[470:800, 140:410]

# â•”â•â•¡ 32e7e51c-dd0d-483d-95cb-e6043f2b2975
md"""
#### Scroll in on Philip's nose!

Use the widgets below (slide left and right sides).
"""

# â•”â•â•¡ 4b64e1f2-d0ca-4e22-a89d-1d9a16bd6788
@bind range_rows RangeSlider(1:size(philip_head)[1])

# â•”â•â•¡ 85919db9-1444-4904-930f-ba572cff9460
@bind range_cols RangeSlider(1:size(philip_head)[2])

# â•”â•â•¡ 2ac47b91-bbc3-49ae-9bf5-4def30ff46f4
nose = philip_head[range_rows, range_cols]

# â•”â•â•¡ 5a0cc342-64c9-11eb-1211-f1b06d652497
md"""
# Process: Modifying an image

Now that we have access to image data, we can start to **process** that data to extract information and/or modify it in some way.

We might want to detect what type of objects are in the image, say to detect whether a patient has a certain disease. To achieve a high-level goal like this, we will need to perform mid-level operations, such as detecting edges that separate different objects based on their color. And, in turn, to carry that out we will need to do low-level operations like comparing colors of neighboring pixels and somehow deciding if they are "different".

"""

# â•”â•â•¡ 4504577c-64c8-11eb-343b-3369b6d10d8b
md"""
## Representing colors

We can  use indexing to *modify* a pixel's color. To do so, we need a way to specify a new color.

Color turns out to be a complicated concept, having to do with the interaction of the physical properties of light with the physiological mechanisms and mental processes by which we detect it!

We will ignore this complexity by using a standard method of representing colours in the computer as an **RGB triple**, i.e. a triple of three numbers $(r, g, b)$, giving the amount of red, of green and of blue in a colour, respectively. These are numbers between 0 (none) and 1 (full). The final colour that we perceive is the result of "adding" the corresponding amount of light of each colour; the details are fascinating, but beyond the scope of this course!
"""

# â•”â•â•¡ 40886d36-64c9-11eb-3c69-4b68673a6dde
md"""
We can create a new color in Julia as follows:
"""

# â•”â•â•¡ 552235ec-64c9-11eb-1f7f-f76da2818cb3
RGB(1.0, 0.0, 0.0)

# â•”â•â•¡ c2907d1a-47b1-4634-8669-a68022706861
begin
	md"""
	A pixel with $(@bind test_r Scrubbable(0:0.1:1; default=0.1)) red, $(@bind test_g Scrubbable(0:0.1:1; default=0.5)) green and $(@bind test_b Scrubbable(0:0.1:1; default=1.0)) blue looks like:
	"""
end
	

# â•”â•â•¡ ff9eea3f-cab0-4030-8337-f519b94316c5
RGB(test_r, test_g, test_b)

# â•”â•â•¡ f6cc03a0-ee07-11ea-17d8-013991514d42
md"""
#### Exercise 2.5
ğŸ‘‰ Write a function `invert` that inverts a color, i.e. sends $(r, g, b)$ to $(1 - r, 1-g, 1-b)$.
"""

# â•”â•â•¡ 63e8d636-ee0b-11ea-173d-bd3327347d55
function invert(color::AbstractRGB)
	
	return missing
end

# â•”â•â•¡ 2cc2f84e-ee0d-11ea-373b-e7ad3204bb00
md"Let's invert some colors:"

# â•”â•â•¡ b8f26960-ee0a-11ea-05b9-3f4bc1099050
black = RGB(0.0, 0.0, 0.0)

# â•”â•â•¡ 5de3a22e-ee0b-11ea-230f-35df4ca3c96d
invert(black)

# â•”â•â•¡ 4e21e0c4-ee0b-11ea-3d65-b311ae3f98e9
red = RGB(0.8, 0.1, 0.1)

# â•”â•â•¡ 6dbf67ce-ee0b-11ea-3b71-abc05a64dc43
invert(red)

# â•”â•â•¡ 846b1330-ee0b-11ea-3579-7d90fafd7290
md"Can you invert the picture of Philip?"

# â•”â•â•¡ 943103e2-ee0b-11ea-33aa-75a8a1529931
philip_inverted = missing

# â•”â•â•¡ 2ee543b2-64d6-11eb-3c39-c5660141787e
md"""

## Modifying a pixel

Let's start by seeing how to modify an image, e.g. in order to hide sensitive information.

We do this by assigning a new value to the color of a pixel:
"""

# â•”â•â•¡ 53bad296-4c7b-471f-b481-0e9423a9288a
let
	temp = copy(philip_head)
	temp[100, 200] = RGB(1.0, 0.0, 0.0)
	temp
end

# â•”â•â•¡ 81b88cbe-64c9-11eb-3b26-39011efb2089
md"""
Be careful: We are actually *modifying* the original image here, even though if we look at the image it is hard to spot, since a single pixel is so small.
"""

# â•”â•â•¡ ab9af0f6-64c9-11eb-13d3-5dbdb75a69a7
md"""
## Groups of pixels

We probably want to examine and modify several pixels at once.
For example, we can extract a horizontal strip 1 pixel tall:
"""

# â•”â•â•¡ e29b7954-64cb-11eb-2768-47de07766055
philip_head[50, 50:100]

# â•”â•â•¡ 8e7c4866-64cc-11eb-0457-85be566a8966
md"""
Here, Julia is showing the strip as a collection of rectangles in a row.


"""

# â•”â•â•¡ f2ad501a-64cb-11eb-1707-3365d05b300a
md"""
And then modify it:
"""

# â•”â•â•¡ 4f03f651-56ed-4361-b954-e6848ac56089
let
	temp = copy(philip_head)
	temp[50, 50:100] .= RGB(1.0, 0.0, 0.0)
	temp
end

# â•”â•â•¡ 2808339c-64cc-11eb-21d1-c76a9854aa5b
md"""
Similarly we can modify a whole rectangular block of pixels:
"""

# â•”â•â•¡ 1bd53326-d705-4d1a-bf8f-5d7f2a4e696f
let
	temp = copy(philip_head)
	temp[50:100, 50:100] .= RGB(1.0, 0.0, 0.0)
	temp
end

# â•”â•â•¡ a5f8bafe-edf0-11ea-0da3-3330861ae43a
md"""
#### Exercise 1.2

ğŸ‘‰ Generate a vector of 100 zeros. Change the center 20 elements to 1.
"""

# â•”â•â•¡ b6b65b94-edf0-11ea-3686-fbff0ff53d08
function create_bar()
	
	return missing
end

# â•”â•â•¡ d862fb16-edf1-11ea-36ec-615d521e6bc0
colored_line(create_bar())

# â•”â•â•¡ e3394c8a-edf0-11ea-1bb8-619f7abb6881
if !@isdefined(create_bar)
	not_defined(:create_bar)
else
	let
		result = create_bar()
		if ismissing(result)
			still_missing()
		elseif isnothing(result)
			keep_working(md"Did you forget to write `return`?")
		elseif !(result isa Vector) || length(result) != 100
			keep_working(md"The result should be a `Vector` with 100 elements.")
		elseif result[[1,50,100]] != [0,1,0]
			keep_working()
		else
			correct()
		end
	end
end

# â•”â•â•¡ 693af19c-64cc-11eb-31f3-57ab2fbae597
md"""
## Reducing the size of an image
"""

# â•”â•â•¡ 6361d102-64cc-11eb-31b7-fb631b632040
md"""
Maybe we would also like to reduce the size of this image, since it's rather large. For example, we could take every 10th row and every 10th column and make a new image from the result:
"""

# â•”â•â•¡ ae542fe4-64cc-11eb-29fc-73b7a66314a9
reduced_image = philip[1:10:end, 1:10:end]

# â•”â•â•¡ c29292b8-64cc-11eb-28db-b52c46e865e6
md"""
Note that the resulting image doesn't look very good, since we seem to have lost too much detail. 

#### Exercise

> Think about what we might do to reduce the size of an image without losing so much detail.
"""

# â•”â•â•¡ 7b04331a-6bcb-11eb-34fa-1f5b151e5510
md"""
# Model: Creating synthetic images 

Think about your favorite Pixar movie (e.g. Monsters Inc.) Movie frames are images that are generated from complicated mathematical models.  Ray tracing (which may be covered in this class)
is a method for making images feel realistic.  
"""

# â•”â•â•¡ 5319c03c-64cc-11eb-0743-a1612476e2d3
md"""
# Output: Saving an image to a file

Finally, we want to be able to save our new creation to a file. To do so, you can **right click** on a displayed image, or you can write it to a file. Fill in a path below:
"""

# â•”â•â•¡ 3db09d92-64cc-11eb-0333-45193c0fd1fe
save("reduced_phil.png", reduced_image)

# â•”â•â•¡ 61606acc-6bcc-11eb-2c80-69ceec9f9702
md"""
# $(html"<br>")   
"""

# â•”â•â•¡ dd183eca-6018-11eb-2a83-2fcaeea62942
md"""
# Computer science: Arrays

An image is a concrete example of a fundamental concept in computer science, namely an **array**. 

Just as an image is a rectangular grid, where each grid cell contains a single color,
an array is a rectangular grid for storing data. Data is stored and retrieved using indexing, just as in the image examples: each cell in the grid can store a single "piece of data" of a given type.


## Dimension of an array

An array can be one-dimensional, like the strip of pixels above, two-dimensional, three-dimensional, and so on. The dimension tells us the number of indices that we need to specify a unique location in the grid.
The array object also needs to know the length of the data in each dimension.

## Names for different types of array

One-dimensional arrays are often called **vectors** (or, in some other languages, "lists") and two-dimensional arrays are **matrices**. Higher-dimensional arrays are  **tensors**.


## Arrays as data structures

An array is an example of a **data structure**, i.e. a way of arranging data such that we can access it. A key theme in computer science is that of designing different data structures that represent data in different ways.

Conceptually, we can think of an array as a block of data that has a position or location in space. This can be a useful way to arrange data if, for example, we want to represent the fact that values in nearby locations in array are somehow near to one another.

Images are a good example of this: neighbouring pixels often represent different pieces of the same object, for example the rug or floor, or Philip himself, in the photo. We thus expect neighbouring pixels to be of a similar color. On the other hand, if they are not, this is also useful information, since that may correspond to the edge of an object.

"""

# â•”â•â•¡ 8ddcb286-602a-11eb-3ae0-07d3c77a0f8c
md"""
# Julia: constructing arrays

## Creating vectors and matrices
Julia has strong support for arrays of any dimension.

Vectors, or one-dimensional arrays, are written using square brackets and commas:
"""

# â•”â•â•¡ f4b0aa23-2d76-4d88-b2a4-3807e88d27ce
[1, 20, "hello"]

# â•”â•â•¡ 1b2b2b18-64d4-11eb-2d43-e31cb8bc25d1
[RGB(1, 0, 0), RGB(0, 1, 0), RGB(0, 0, 1)]

# â•”â•â•¡ 2b0e6450-64d4-11eb-182b-ff1bd515b56f
md"""
Matrices, or two-dimensional arrays, also use square brackets, but with spaces and new lines instead of commas:
"""

# â•”â•â•¡ 3b2b041a-64d4-11eb-31dd-47d7321ee909
[RGB(1, 0, 0)  RGB(0, 1, 0)
 RGB(0, 0, 1)  RGB(0.5, 0.5, 0.5)]

# â•”â•â•¡ 0f35603a-64d4-11eb-3baf-4fef06d82daa
md"""

## Array comprehensions

It's clear that if we want to create an array with more than a few elements, it will be *very* tedious to do so by hand like this.
Rather, we want to *automate* the process of creating an array by following some pattern, for example to create a whole palette of colors!

Let's start with all the possible colors interpolating between black, `RGB(0, 0, 0)`, and red, `RGB(1, 0, 0)`.  Since only one of the values is changing, we can represent this as a vector, i.e. a one-dimensional array.

A neat method to do this is an **array comprehension**. Again we use square brackets  to create an array, but now we use a **variable** that varies over a given **range** values:
"""

# â•”â•â•¡ e69b02c6-64d6-11eb-02f1-21c4fb5d1043
[RGB(x, 0, 0) for x in 0:0.1:1]

# â•”â•â•¡ fce76132-64d6-11eb-259d-b130038bbae6
md"""
Here, `0:0.1:1` is a **range**; the first and last numbers are the start and end values, and the middle number is the size of the step.
"""

# â•”â•â•¡ 17a69736-64d7-11eb-2c6c-eb5ebf51b285
md"""
In a similar way we can create two-dimensional matrices, by separating the two variables for each dimension with a comma (`,`):
"""

# â•”â•â•¡ 291b04de-64d7-11eb-1ee0-d998dccb998c
[RGB(i, j, 0) for i in 0:0.1:1, j in 0:0.1:1]

# â•”â•â•¡ 647fddf2-60ee-11eb-124d-5356c7014c3b
md"""
## Joining matrices

We often want to join vectors and matrices together. We can do so using an extension of the array creation syntax:
"""

# â•”â•â•¡ 7d9ad134-60ee-11eb-1b2a-a7d63f3a7a2d
[philip_head  philip_head]

# â•”â•â•¡ 8433b862-60ee-11eb-0cfc-add2b72997dc
[philip_head                   reverse(philip_head, dims=2)
 reverse(philip_head, dims=1)  rot180(philip_head)]

# â•”â•â•¡ 5e52d12e-64d7-11eb-0905-c9038a404e24
md"""
# Pluto: Interactivity using sliders
"""

# â•”â•â•¡ 6aba7e62-64d7-11eb-2c49-7944e9e2b94b
md"""
Suppose we want to see the effect of changing the number of colors in our vector or matrix. We could, of course, do so by manually fiddling with the range.

It would be nice if we could do so using a **user interface**, for example with a **slider**. Fortunately, the Pluto notebook allows us to do so!
"""

# â•”â•â•¡ afc66dac-64d7-11eb-1ad0-7f62c20ffefb
md"""
We can define a slider using
"""

# â•”â•â•¡ b37c9868-64d7-11eb-3033-a7b5d3065f7f
@bind number_reds Slider(1:100, show_value=true)

# â•”â•â•¡ b1dfe122-64dc-11eb-1104-1b8852b2c4c5
md"""
[The `Slider` type is defined in the `PlutoUI.jl` package.]
"""

# â•”â•â•¡ cfc55140-64d7-11eb-0ff6-e59c70d01d67
md"""
This creates a new variable called `number_reds`, whose value is the value shown by the slider. When we move the slider, the value of the variable gets updated. Since Pluto is a **reactive** notebook, other cells which use the value of this variable will *automatically be updated too*!
"""

# â•”â•â•¡ fca72490-64d7-11eb-1464-f5e0582c4d18
md"""
Let's use this to make a slider for our one-dimensional collection of reds:
"""

# â•”â•â•¡ 88933746-6028-11eb-32de-13eb6ff43e29
[RGB(red_value / number_reds, 0, 0) for red_value in 0:number_reds]

# â•”â•â•¡ 1c539b02-64d8-11eb-3505-c9288357d139
md"""
When you move the slider, you should see the number of red color patches change!
"""

# â•”â•â•¡ 10f6e6da-64d8-11eb-366f-11f16e73043b
md"""
What is going on here is that we are creating a vector in which `red_value` takes each value in turn from the range from `0` up to the current value of `number_reds`. If we change `number_reds`, then we create a new vector with that new number of red patches.
"""

# â•”â•â•¡ 82a8314c-64d8-11eb-1acb-e33625381178
md"""
#### Exercise

> Make three sliders with variables `r`, `g` and `b`. Then make a single color patch with the RGB color given by those values.
"""

# â•”â•â•¡ 576d5e3a-64d8-11eb-10c9-876be31f7830
md"""
We can do the same to create different size matrices, by creating two sliders, one for reds and one for greens. Try it out!
"""

# â•”â•â•¡ 2a94a2cf-b697-4b0b-afd0-af2e35af2bb1
@bind webcam_data camera_input()

# â•”â•â•¡ 3e0ece65-b8a7-4be7-ae44-6d7210c2e15b
myface = process_raw_camera_data(webcam_data);

# â•”â•â•¡ 4ee18bee-13e6-4478-b2ca-ab66100e57ec
[
	myface              myface[   :    , end:-1:1]
	myface[end:-1:1, :] myface[end:-1:1, end:-1:1]
]

# â•”â•â•¡ ace86c8a-60ee-11eb-34ef-93c54abc7b1a
md"""
# Summary
"""

# â•”â•â•¡ b08e57e4-60ee-11eb-0e1a-2f49c496668b
md"""
Let's summarize the main ideas from this notebook:
- Images are **arrays** of colors
- We can inspect and modify arrays using **indexing**
- We can create arrays directly or using **array comprehensions**
"""

# â•”â•â•¡ 9025a5b4-6066-11eb-20e8-099e9b8f859e
md"""
----
"""

# â•”â•â•¡ 45815734-ee0a-11ea-2982-595e1fc0e7b1
bigbreak

# â•”â•â•¡ 5da8cbe8-eded-11ea-2e43-c5b7cc71e133
begin
	colored_line(x::Vector{<:Real}) = Gray.(Float64.((hcat(x)')))
	colored_line(x::Any) = nothing
end

# â•”â•â•¡ e074560a-601b-11eb-340e-47acd64f03b2
hint(text) = Markdown.MD(Markdown.Admonition("hint", "Hint", [text]))

# â•”â•â•¡ e0776548-601b-11eb-2563-57ba2cf1d5d1
almost(text) = Markdown.MD(Markdown.Admonition("warning", "Almost there!", [text]))

# â•”â•â•¡ e083bef6-601b-11eb-2134-e3063d5c4253
still_missing(text=md"Replace `missing` with your answer.") = Markdown.MD(Markdown.Admonition("warning", "Here we go!", [text]))

# â•”â•â•¡ e08ecb84-601b-11eb-0e25-152ed3a262f7
keep_working(text=md"The answer is not quite right.") = Markdown.MD(Markdown.Admonition("danger", "Keep working on it!", [text]))

# â•”â•â•¡ e09036a4-601b-11eb-1a8b-ef70105ab91c
yays = [md"Great!", md"Yay â¤", md"Great! ğŸ‰", md"Well done!", md"Keep it up!", md"Good job!", md"Awesome!", md"You got the right answer!", md"Let's move on to the next section."]

# â•”â•â•¡ e09af1a2-601b-11eb-14c8-57a46546f6ce
correct(text=rand(yays)) = Markdown.MD(Markdown.Admonition("correct", "Got it!", [text]))

# â•”â•â•¡ e0a4fc10-601b-11eb-211d-03570aca2726
not_defined(variable_name) = Markdown.MD(Markdown.Admonition("danger", "Oopsie!", [md"Make sure that you define a variable called **$(Markdown.Code(string(variable_name)))**"]))

# â•”â•â•¡ e0a6031c-601b-11eb-27a5-65140dd92897
bigbreak = html"<br><br><br><br><br>";

# â•”â•â•¡ e0b15582-601b-11eb-26d6-bbf708933bc8
function camera_input(;max_size=150, default_url="https://i.imgur.com/SUmi94P.png")
"""
<span class="pl-image waiting-for-permission">
<style>
	
	.pl-image.popped-out {
		position: fixed;
		top: 0;
		right: 0;
		z-index: 5;
	}

	.pl-image #video-container {
		width: 250px;
	}

	.pl-image video {
		border-radius: 1rem 1rem 0 0;
	}
	.pl-image.waiting-for-permission #video-container {
		display: none;
	}
	.pl-image #prompt {
		display: none;
	}
	.pl-image.waiting-for-permission #prompt {
		width: 250px;
		height: 200px;
		display: grid;
		place-items: center;
		font-family: monospace;
		font-weight: bold;
		text-decoration: underline;
		cursor: pointer;
		border: 5px dashed rgba(0,0,0,.5);
	}

	.pl-image video {
		display: block;
	}
	.pl-image .bar {
		width: inherit;
		display: flex;
		z-index: 6;
	}
	.pl-image .bar#top {
		position: absolute;
		flex-direction: column;
	}
	
	.pl-image .bar#bottom {
		background: black;
		border-radius: 0 0 1rem 1rem;
	}
	.pl-image .bar button {
		flex: 0 0 auto;
		background: rgba(255,255,255,.8);
		border: none;
		width: 2rem;
		height: 2rem;
		border-radius: 100%;
		cursor: pointer;
		z-index: 7;
	}
	.pl-image .bar button#shutter {
		width: 3rem;
		height: 3rem;
		margin: -1.5rem auto .2rem auto;
	}

	.pl-image video.takepicture {
		animation: pictureflash 200ms linear;
	}

	@keyframes pictureflash {
		0% {
			filter: grayscale(1.0) contrast(2.0);
		}

		100% {
			filter: grayscale(0.0) contrast(1.0);
		}
	}
</style>

	<div id="video-container">
		<div id="top" class="bar">
			<button id="stop" title="Stop video">âœ–</button>
			<button id="pop-out" title="Pop out/pop in">â</button>
		</div>
		<video playsinline autoplay></video>
		<div id="bottom" class="bar">
		<button id="shutter" title="Click to take a picture">ğŸ“·</button>
		</div>
	</div>
		
	<div id="prompt">
		<span>
		Enable webcam
		</span>
	</div>

<script>
	// based on https://github.com/fonsp/printi-static (by the same author)

	const span = currentScript.parentElement
	const video = span.querySelector("video")
	const popout = span.querySelector("button#pop-out")
	const stop = span.querySelector("button#stop")
	const shutter = span.querySelector("button#shutter")
	const prompt = span.querySelector(".pl-image #prompt")

	const maxsize = $(max_size)

	const send_source = (source, src_width, src_height) => {
		const scale = Math.min(1.0, maxsize / src_width, maxsize / src_height)

		const width = Math.floor(src_width * scale)
		const height = Math.floor(src_height * scale)

		const canvas = html`<canvas width=\${width} height=\${height}>`
		const ctx = canvas.getContext("2d")
		ctx.drawImage(source, 0, 0, width, height)

		span.value = {
			width: width,
			height: height,
			data: ctx.getImageData(0, 0, width, height).data,
		}
		span.dispatchEvent(new CustomEvent("input"))
	}
	
	const clear_camera = () => {
		window.stream.getTracks().forEach(s => s.stop());
		video.srcObject = null;

		span.classList.add("waiting-for-permission");
	}

	prompt.onclick = () => {
		navigator.mediaDevices.getUserMedia({
			audio: false,
			video: {
				facingMode: "environment",
			},
		}).then(function(stream) {

			stream.onend = console.log

			window.stream = stream
			video.srcObject = stream
			window.cameraConnected = true
			video.controls = false
			video.play()
			video.controls = false

			span.classList.remove("waiting-for-permission");

		}).catch(function(error) {
			console.log(error)
		});
	}
	stop.onclick = () => {
		clear_camera()
	}
	popout.onclick = () => {
		span.classList.toggle("popped-out")
	}

	shutter.onclick = () => {
		const cl = video.classList
		cl.remove("takepicture")
		void video.offsetHeight
		cl.add("takepicture")
		video.play()
		video.controls = false
		console.log(video)
		send_source(video, video.videoWidth, video.videoHeight)
	}
	
	
	document.addEventListener("visibilitychange", () => {
		if (document.visibilityState != "visible") {
			clear_camera()
		}
	})


	// Set a default image

	const img = html`<img crossOrigin="anonymous">`

	img.onload = () => {
	console.log("helloo")
		send_source(img, img.width, img.height)
	}
	img.src = "$(default_url)"
	console.log(img)
</script>
</span>
""" |> HTML
end

# â•”â•â•¡ e891fce0-601b-11eb-383b-bde5b128822e

function process_raw_camera_data(raw_camera_data)
	# the raw image data is a long byte array, we need to transform it into something
	# more "Julian" - something with more _structure_.
	
	# The encoding of the raw byte stream is:
	# every 4 bytes is a single pixel
	# every pixel has 4 values: Red, Green, Blue, Alpha
	# (we ignore alpha for this notebook)
	
	# So to get the red values for each pixel, we take every 4th value, starting at 
	# the 1st:
	reds_flat = UInt8.(raw_camera_data["data"][1:4:end])
	greens_flat = UInt8.(raw_camera_data["data"][2:4:end])
	blues_flat = UInt8.(raw_camera_data["data"][3:4:end])
	
	# but these are still 1-dimensional arrays, nicknamed 'flat' arrays
	# We will 'reshape' this into 2D arrays:
	
	width = raw_camera_data["width"]
	height = raw_camera_data["height"]
	
	# shuffle and flip to get it in the right shape
	reds = reshape(reds_flat, (width, height))' / 255.0
	greens = reshape(greens_flat, (width, height))' / 255.0
	blues = reshape(blues_flat, (width, height))' / 255.0
	
	# we have our 2D array for each color
	# Let's create a single 2D array, where each value contains the R, G and B value of 
	# that pixel
	
	RGB.(reds, greens, blues)
end

# â•”â•â•¡ 3ef77236-1867-4d02-8af2-ff4777fcd6d9
exercise_css = html"""
<style>

ct-exercise > h4 {
    background: #73789a;
    color: white;
    padding: 0rem 1.5rem;
    font-size: 1.2rem;
    border-radius: .6rem .6rem 0rem 0rem;
	margin-left: .5rem;
	display: inline-block;
}
ct-exercise > section {
	    background: #31b3ff1a;
    border-radius: 0rem 1rem 1rem 1rem;
    padding: .7rem;
    margin: .5rem;
    margin-top: 0rem;
    position: relative;
}


/*ct-exercise > section::before {
	content: "ğŸ‘‰";
    display: block;
    position: absolute;
    left: 0px;
    top: 0px;
    background: #ffffff8c;
    border-radius: 100%;
    width: 1rem;
    height: 1rem;
    padding: .5rem .5rem;
    font-size: 1rem;
    line-height: 1rem;
    left: -1rem;
}*/


ct-answer {
	display: flex;
}
</style>

"""

# â•”â•â•¡ 61b29e7d-5aba-4bc8-870b-c1c43919c236
exercise(x, number="") = 
@htl("""
	<ct-exercise class="exercise">
	<h4>Exercise <span>$(number)</span></h4>
	<section>$(x)
	</section>
	</ct-exercise>
	""")

# â•”â•â•¡ a9fef6c9-e911-4d8c-b141-a4832b40a260
quick_question(x, number, options, correct) = let
	name = join(rand('a':'z',16))
@htl("""
	<ct-exercise class="quick-question">
	<h4>Quick Question <span>$(number)</span></h4>
	<section>$(x)
	<ct-answers>
	$(map(enumerate(options)) do (i, o)
		@htl("<ct-answer><input type=radio name=$(name) id=$(i) >$(o)</ct-answer>")
	end)
	</ct-answers>
	</section>
	</ct-exercise>
	""")
end

# â•”â•â•¡ edf900be-601b-11eb-0456-3f7cfc5e876b
md"_Lecture 1, Spring 2021, version 0_"

# â•”â•â•¡ Cell order:
# â•Ÿâ”€e91d7926-ec6e-41e7-aba2-9dca333c8aa5
# â•Ÿâ”€d07fcdb0-7afc-4a25-b68a-49fd1e3405e7
# â•Ÿâ”€9b49500c-0164-4556-a17b-7595e35c5ede
# â• â•74b008f6-ed6b-11ea-291f-b3791d6d1b35
# â•Ÿâ”€71a1e08a-6abc-48d5-b177-5184dbdd76a8
# â•Ÿâ”€ca1b507e-6017-11eb-34e6-6b85cd189002
# â•Ÿâ”€e9ff96d8-6bc1-11eb-0f6a-234b9fae047e
# â•Ÿâ”€9111db10-6bc3-11eb-38e5-cf3f58536914
# â•Ÿâ”€fb8a99ac-6bc1-11eb-0835-3146734a1c99
# â•Ÿâ”€b795dcb4-6bc3-11eb-20ec-db2cc4b89bfb
# â•Ÿâ”€8691e434-6bc4-11eb-07d1-8169158484e6
# â•Ÿâ”€546db74c-6d4e-11eb-2e27-f5bed9dbd9ba
# â•Ÿâ”€6385d174-6d4e-11eb-093b-6f6fafb79f84
# â•Ÿâ”€132f6596-6bc6-11eb-29f1-1b2478c929af
# â•Ÿâ”€635a03dd-abd7-49c8-a3d2-e68c7d83cc9b
# â•Ÿâ”€9eb6efd2-6018-11eb-2db8-c3ce41d9e337
# â•Ÿâ”€e37e4d40-6018-11eb-3e1d-093266c98507
# â•Ÿâ”€e1c9742a-6018-11eb-23ba-d974e57f78f9
# â•Ÿâ”€9b004f70-6bc9-11eb-128c-914eadfc9a0e
# â•Ÿâ”€62fa19da-64c6-11eb-0038-5d40a6890cf5
# â• â•34ee0954-601e-11eb-1912-97dc2937fd52
# â•Ÿâ”€9180fbcc-601e-11eb-0c22-c920dc7ee9a9
# â• â•34ffc3d8-601e-11eb-161c-6f9a07c5fd78
# â•Ÿâ”€abaaa980-601e-11eb-0f71-8ff02269b775
# â• â•aafe76a6-601e-11eb-1ff5-01885c5238da
# â•Ÿâ”€e86ed944-ee05-11ea-3e0f-d70fc73b789c
# â•Ÿâ”€c99d2aa8-601e-11eb-3469-497a246db17c
# â•Ÿâ”€11dff4ce-6bca-11eb-1056-c1345c796ed4
# â•Ÿâ”€efef3a32-6bc9-11eb-17e9-dd2171be9c21
# â•Ÿâ”€e94dcc62-6d4e-11eb-3d53-ff9878f0091e
# â•Ÿâ”€d6742ea0-1106-4f3c-a5b8-a31a48d33f19
# â• â•1d7375b7-7ea6-4d67-ab73-1c69d6b8b87f
# â• â•6224c74b-8915-4983-abf0-30e6ba04a46d
# â•Ÿâ”€cef1a95a-64c6-11eb-15e7-636a3621d727
# â•Ÿâ”€f26d9326-64c6-11eb-1166-5d82586422ed
# â•Ÿâ”€6f928b30-602c-11eb-1033-71d442feff93
# â• â•75c5c85a-602c-11eb-2fb1-f7e7f2c5d04b
# â•Ÿâ”€77f93eb8-602c-11eb-1f38-efa56cc93ca5
# â• â•96b7d801-c427-4e27-ab1f-e2fd18fc24d0
# â• â•f08d02af-6e38-4ace-8b11-7af4930b64ea
# â•Ÿâ”€f9244264-64c6-11eb-23a6-cfa76f8aff6d
# â• â•bd22d09a-64c7-11eb-146f-67733b8be241
# â•Ÿâ”€28860d48-64c8-11eb-240f-e1232b3638df
# â•Ÿâ”€4ef99715-4d8d-4f9d-bf0b-8df9907a14cf
# â•Ÿâ”€a510fc33-406e-4fb5-be83-9e4b5578717c
# â• â•94b77934-713e-11eb-18cf-c5dc5e7afc5b
# â• â•ff762861-b186-4eb0-9582-0ce66ca10f60
# â•Ÿâ”€13844ebf-52c4-47e9-bda4-106a02fad9d7
# â• â•08d61afb-c641-4aa9-b995-2552af89f3b8
# â• â•6511a498-7ac9-445b-9c15-ec02d09783fe
# â•Ÿâ”€c9ed950c-dcd9-4296-a431-ee0f36d5b557
# â• â•f0796032-8105-4f6d-b5ee-3647b052f2f6
# â•Ÿâ”€b9be8761-a9c9-49eb-ba1b-527d12097362
# â• â•d515286b-4ad4-449b-8967-06b9b4c87684
# â•Ÿâ”€eef8fbc8-8887-4628-8ba8-114575d6b91f
# â• â•4e6a31d6-1ef8-4a69-b346-ad58cfc4d8a5
# â•Ÿâ”€e11f0e47-02d9-48a6-9b1a-e313c18db129
# â• â•9e447eab-14b6-45d8-83ab-1f7f1f1c70d2
# â• â•c926435c-c648-419c-9951-ac8a1d4f3b92
# â•Ÿâ”€32e7e51c-dd0d-483d-95cb-e6043f2b2975
# â• â•4b64e1f2-d0ca-4e22-a89d-1d9a16bd6788
# â• â•85919db9-1444-4904-930f-ba572cff9460
# â• â•2ac47b91-bbc3-49ae-9bf5-4def30ff46f4
# â•Ÿâ”€5a0cc342-64c9-11eb-1211-f1b06d652497
# â•Ÿâ”€4504577c-64c8-11eb-343b-3369b6d10d8b
# â•Ÿâ”€40886d36-64c9-11eb-3c69-4b68673a6dde
# â• â•552235ec-64c9-11eb-1f7f-f76da2818cb3
# â•Ÿâ”€c2907d1a-47b1-4634-8669-a68022706861
# â• â•ff9eea3f-cab0-4030-8337-f519b94316c5
# â•Ÿâ”€f6cc03a0-ee07-11ea-17d8-013991514d42
# â• â•63e8d636-ee0b-11ea-173d-bd3327347d55
# â•Ÿâ”€2cc2f84e-ee0d-11ea-373b-e7ad3204bb00
# â•Ÿâ”€b8f26960-ee0a-11ea-05b9-3f4bc1099050
# â• â•5de3a22e-ee0b-11ea-230f-35df4ca3c96d
# â• â•4e21e0c4-ee0b-11ea-3d65-b311ae3f98e9
# â• â•6dbf67ce-ee0b-11ea-3b71-abc05a64dc43
# â•Ÿâ”€846b1330-ee0b-11ea-3579-7d90fafd7290
# â• â•943103e2-ee0b-11ea-33aa-75a8a1529931
# â•Ÿâ”€2ee543b2-64d6-11eb-3c39-c5660141787e
# â• â•53bad296-4c7b-471f-b481-0e9423a9288a
# â•Ÿâ”€81b88cbe-64c9-11eb-3b26-39011efb2089
# â•Ÿâ”€ab9af0f6-64c9-11eb-13d3-5dbdb75a69a7
# â• â•e29b7954-64cb-11eb-2768-47de07766055
# â•Ÿâ”€8e7c4866-64cc-11eb-0457-85be566a8966
# â•Ÿâ”€f2ad501a-64cb-11eb-1707-3365d05b300a
# â• â•4f03f651-56ed-4361-b954-e6848ac56089
# â•Ÿâ”€2808339c-64cc-11eb-21d1-c76a9854aa5b
# â• â•1bd53326-d705-4d1a-bf8f-5d7f2a4e696f
# â•Ÿâ”€a5f8bafe-edf0-11ea-0da3-3330861ae43a
# â• â•b6b65b94-edf0-11ea-3686-fbff0ff53d08
# â•Ÿâ”€d862fb16-edf1-11ea-36ec-615d521e6bc0
# â•Ÿâ”€e3394c8a-edf0-11ea-1bb8-619f7abb6881
# â•Ÿâ”€693af19c-64cc-11eb-31f3-57ab2fbae597
# â•Ÿâ”€6361d102-64cc-11eb-31b7-fb631b632040
# â• â•ae542fe4-64cc-11eb-29fc-73b7a66314a9
# â•Ÿâ”€c29292b8-64cc-11eb-28db-b52c46e865e6
# â•Ÿâ”€7b04331a-6bcb-11eb-34fa-1f5b151e5510
# â•Ÿâ”€5319c03c-64cc-11eb-0743-a1612476e2d3
# â• â•3db09d92-64cc-11eb-0333-45193c0fd1fe
# â•Ÿâ”€61606acc-6bcc-11eb-2c80-69ceec9f9702
# â•Ÿâ”€dd183eca-6018-11eb-2a83-2fcaeea62942
# â•Ÿâ”€8ddcb286-602a-11eb-3ae0-07d3c77a0f8c
# â• â•f4b0aa23-2d76-4d88-b2a4-3807e88d27ce
# â• â•1b2b2b18-64d4-11eb-2d43-e31cb8bc25d1
# â•Ÿâ”€2b0e6450-64d4-11eb-182b-ff1bd515b56f
# â• â•3b2b041a-64d4-11eb-31dd-47d7321ee909
# â•Ÿâ”€0f35603a-64d4-11eb-3baf-4fef06d82daa
# â• â•e69b02c6-64d6-11eb-02f1-21c4fb5d1043
# â•Ÿâ”€fce76132-64d6-11eb-259d-b130038bbae6
# â•Ÿâ”€17a69736-64d7-11eb-2c6c-eb5ebf51b285
# â• â•291b04de-64d7-11eb-1ee0-d998dccb998c
# â•Ÿâ”€647fddf2-60ee-11eb-124d-5356c7014c3b
# â• â•7d9ad134-60ee-11eb-1b2a-a7d63f3a7a2d
# â• â•8433b862-60ee-11eb-0cfc-add2b72997dc
# â•Ÿâ”€5e52d12e-64d7-11eb-0905-c9038a404e24
# â•Ÿâ”€6aba7e62-64d7-11eb-2c49-7944e9e2b94b
# â•Ÿâ”€afc66dac-64d7-11eb-1ad0-7f62c20ffefb
# â• â•b37c9868-64d7-11eb-3033-a7b5d3065f7f
# â•Ÿâ”€b1dfe122-64dc-11eb-1104-1b8852b2c4c5
# â•Ÿâ”€cfc55140-64d7-11eb-0ff6-e59c70d01d67
# â•Ÿâ”€fca72490-64d7-11eb-1464-f5e0582c4d18
# â• â•88933746-6028-11eb-32de-13eb6ff43e29
# â•Ÿâ”€1c539b02-64d8-11eb-3505-c9288357d139
# â•Ÿâ”€10f6e6da-64d8-11eb-366f-11f16e73043b
# â•Ÿâ”€82a8314c-64d8-11eb-1acb-e33625381178
# â•Ÿâ”€576d5e3a-64d8-11eb-10c9-876be31f7830
# â• â•2a94a2cf-b697-4b0b-afd0-af2e35af2bb1
# â• â•3e0ece65-b8a7-4be7-ae44-6d7210c2e15b
# â• â•4ee18bee-13e6-4478-b2ca-ab66100e57ec
# â•Ÿâ”€ace86c8a-60ee-11eb-34ef-93c54abc7b1a
# â•Ÿâ”€b08e57e4-60ee-11eb-0e1a-2f49c496668b
# â•Ÿâ”€9025a5b4-6066-11eb-20e8-099e9b8f859e
# â•Ÿâ”€45815734-ee0a-11ea-2982-595e1fc0e7b1
# â•Ÿâ”€5da8cbe8-eded-11ea-2e43-c5b7cc71e133
# â•Ÿâ”€e074560a-601b-11eb-340e-47acd64f03b2
# â•Ÿâ”€e0776548-601b-11eb-2563-57ba2cf1d5d1
# â•Ÿâ”€e083bef6-601b-11eb-2134-e3063d5c4253
# â•Ÿâ”€e08ecb84-601b-11eb-0e25-152ed3a262f7
# â•Ÿâ”€e09036a4-601b-11eb-1a8b-ef70105ab91c
# â•Ÿâ”€e09af1a2-601b-11eb-14c8-57a46546f6ce
# â•Ÿâ”€e0a4fc10-601b-11eb-211d-03570aca2726
# â• â•e0a6031c-601b-11eb-27a5-65140dd92897
# â•Ÿâ”€e0b15582-601b-11eb-26d6-bbf708933bc8
# â•Ÿâ”€e891fce0-601b-11eb-383b-bde5b128822e
# â•Ÿâ”€3ef77236-1867-4d02-8af2-ff4777fcd6d9
# â•Ÿâ”€61b29e7d-5aba-4bc8-870b-c1c43919c236
# â•Ÿâ”€a9fef6c9-e911-4d8c-b141-a4832b40a260
# â•Ÿâ”€edf900be-601b-11eb-0456-3f7cfc5e876b
