### A Pluto.jl notebook ###
# v0.12.20

using Markdown
using InteractiveUtils

# This Pluto notebook uses @bind for interactivity. When running this notebook outside of Pluto, the following 'mock version' of @bind gives bound variables a default value (instead of an error).
macro bind(def, element)
    quote
        local el = $(esc(element))
        global $(esc(def)) = Core.applicable(Base.get, el) ? Base.get(el) : missing
        el
    end
end

# â•”â•â•¡ 74b008f6-ed6b-11ea-291f-b3791d6d1b35
begin
	import Pkg
	Pkg.activate(mktempdir())
	Pkg.add(["Images", "Colors", "PlutoUI"])

	using Images
	using Colors
	using PlutoUI
end

# â•”â•â•¡ e91d7926-ec6e-41e7-aba2-9dca333c8aa5
html"""

<div style="margin: 5rem 0;">
<h3> <em>Chapter 1</em> </h3>
<h1 style="text-align: center; "><em>Working with images<br>and arrays</em></h1>
</div>

"""

# â•”â•â•¡ d07fcdb0-7afc-4a25-b68a-49fd1e3405e7
PlutoUI.TableOfContents(aside=true)

# â•”â•â•¡ ca1b507e-6017-11eb-34e6-6b85cd189002
md"""
# Introduction
Welcome to the Computational Thinking using Julia for Real-World Problems, at MIT in Spring 2021!

The aim of this course is to bring together concepts from computer science and applied math with coding in the modern **Julia language**, and to see how to apply these techniques to study interesting applications.

## Data are all around us
Applications of computer science in the real world use **data**, i.e. information that we can **measure** in some way. Data take many different forms, for example:

- Numbers that change over time (**time series**): 
  - Stock price each second / minute / day
  - Weekly number of infections

- Video:
  - The view from a window of a self-driving car
  - A hurricane monitoring station

- Images:
  - Diseased versus healthy tissue in a medical scan
  - Your social media account labelling your friends in your photos

#### Exercise: 
> Think of another two examples in each category. Can you think of other categories of data?


To use any data source, we need to **input** the data of interest, for example by downloading it, reading in the resulting file, and converting it into a form that we can use in the computer. Then we need to **process** it in some way to extract information of interest. We usually want to **visualize** the results, and we may want to **output** them, for example by saving to disc or putting them on a website.

We also may want to make a mathematical or computational **model** that can help us to understand and predict the behavior of the system of interest.

In this course we aim to show how programming, computer science and applied math combine to help us with these goals.
"""

# â•”â•â•¡ 127daf08-601b-11eb-28c1-2139c8d1a65a
md"""

# Images 

Let's start off by looking at **images** and how we can process them. 
Our goal is to process the data contained in an image in some way, which we will do by developing and coding certain **algorithms**.

To begin, though, let's take advantage of the fact that Julia already has various tools that have been written for more mundane tasks, such as converting an image input file from its particular format into numerical data that we can use.
"""

# â•”â•â•¡ 9eb6efd2-6018-11eb-2db8-c3ce41d9e337
md"""
## What is an image? 

If we open an image on our computer or the web and zoom in enough, we will see that it consists of many tiny squares, or **pixels** ("picture elements"). Each pixel is a block of one single colour, and the pixels are arranged in a two-dimensional square grid. 

Note that an image is already an **approximation** of the real world -- it is a two-dimensional, discrete representation of a three-dimensional reality.

"""

# â•”â•â•¡ e37e4d40-6018-11eb-3e1d-093266c98507
md"""
## Julia: Reading in an image file
"""

# â•”â•â•¡ e1c9742a-6018-11eb-23ba-d974e57f78f9
md"""
To make things more concrete, let's use Julia to load in an actual image and play around with it.

One nice option is to use your computer's webcam to take a photo of yourself; another is to download one from the web.

We can use the `Images.jl` package to load an image file.
"""

# â•”â•â•¡ 62fa19da-64c6-11eb-0038-5d40a6890cf5
md"""
First we specify the URL to download from:
"""

# â•”â•â•¡ 34ee0954-601e-11eb-1912-97dc2937fd52
url = "https://i.imgur.com/VGPeJ6s.jpg" 

# â•”â•â•¡ 9180fbcc-601e-11eb-0c22-c920dc7ee9a9
md"""
Now we use the aptly-named `download` function to download the image file to our own computer:
"""

# â•”â•â•¡ 34ffc3d8-601e-11eb-161c-6f9a07c5fd78
download(url, "philip.jpg")  # download to a local file

# â•”â•â•¡ abaaa980-601e-11eb-0f71-8ff02269b775
md"""
Using the `Images.jl` package we can **load** the file, which automatically converts it into usable data. We'll store the result in a variable:
"""

# â•”â•â•¡ aafe76a6-601e-11eb-1ff5-01885c5238da
my_image = load("philip.jpg")

# â•”â•â•¡ c99d2aa8-601e-11eb-3469-497a246db17c
md"""
We see that the Pluto notebook has recognised that we created an object representing an image, and automatically displayed the resulting image of Philip, the cute Welsh Pembroke corgi and co-professor of this course.
Poor Philip will undergo quite a few transformations as we go along!
"""

# â•”â•â•¡ cef1a95a-64c6-11eb-15e7-636a3621d727
md"""
## Working with images
"""

# â•”â•â•¡ f26d9326-64c6-11eb-1166-5d82586422ed
md"""
### Image size
"""

# â•”â•â•¡ 6f928b30-602c-11eb-1033-71d442feff93
md"""
The first thing we might want to know is the size of the image:
"""

# â•”â•â•¡ 75c5c85a-602c-11eb-2fb1-f7e7f2c5d04b
size(my_image)

# â•”â•â•¡ 77f93eb8-602c-11eb-1f38-efa56cc93ca5
md"""
Julia returns a pair of two numbers. Comparing these with the picture of the image, we see that the first number is the height, i.e. the vertical number of pixels, and the second is the width.
"""

# â•”â•â•¡ f9244264-64c6-11eb-23a6-cfa76f8aff6d
md"""
### Locations in an image: Indexing

Now suppose that we want to examine a piece of the image in more detail. We need some way of specifying which piece of the image we want. 

Thinking of the image as a grid of pixels, we need a way to tell the computer which pixel or group of pixels we want to refer to. 
Since the image is a two-dimensional grid, we can use two integers (whole numbers) to give the coordinates of a single pixel.  Specifying coordinates like this is called **indexing**: think of the index of a book, which tells you *on which page* an idea is discussed.

In Julia we use (square) brackets, `[` and `]` for indexing: 
"""

# â•”â•â•¡ bd22d09a-64c7-11eb-146f-67733b8be241
a_pixel = my_image[1000, 1500]

# â•”â•â•¡ 28860d48-64c8-11eb-240f-e1232b3638df
md"""
We see that Julia knows to draw our pixel object for us a block of the relevant color.
"""

# â•”â•â•¡ 622f514e-64c8-11eb-0c67-c7940e894973
md"""
When we index into an image like this, the first number indicates the *row* in the image, starting from the top, and the second the *column*, starting from the left. In Julia, the first row and column are numbered starting from 1, not from 0 as in some other programming languages.
"""

# â•”â•â•¡ 4504577c-64c8-11eb-343b-3369b6d10d8b
md"""
### Representing colors

We can also use indexing to *modify* a pixel's color. To do so we need a way to specify a new color.

Color turns out to be a complicated concept, having to do with the interaction of the physical properties of light with the physiological mechanisms and mental processes by which we detect it!

We will ignore this complexity by using a standard method of representing colours in the computer as an **RGB triple**, i.e. a triple of three numbers $(r, g, b)$, giving the amount of red, of green and of blue in a colour, respectively. These are numbers between 0 (none) and 1 (full). The final colour that we perceive is the result of "adding" the corresponding amount of light of each colour; the details are fascinating, but beyond the scope of this course!
"""

# â•”â•â•¡ 40886d36-64c9-11eb-3c69-4b68673a6dde
md"""
We can create a new color in Julia as follows:
"""

# â•”â•â•¡ 552235ec-64c9-11eb-1f7f-f76da2818cb3
RGB(1.0, 0.0, 0.0)

# â•”â•â•¡ 5a0cc342-64c9-11eb-1211-f1b06d652497
md"""
# Image processing

Now that we have access to image data, we can start to **process** that data to extract information and/or modify it in some way.

We might want to detect what type of objects are in the image, say to detect whether a patient has a certain disease. To achieve a high-level goal like this, we will need to perform mid-level operations, such as detecting edges that separate different objects based on their color. And, in turn, to carry that out we will need to do low-level operations like comparing colors of neighboring pixels and somehow deciding if they are "different".

"""

# â•”â•â•¡ 2ee543b2-64d6-11eb-3c39-c5660141787e
md"""

## Modifying a pixel

Let's start by seeing how to modify an image, e.g. in order to hide sensitive information.

We do this by assigning a new value to the color of a pixel:
"""

# â•”â•â•¡ 62da7c94-64c9-11eb-10dd-a374ce105d81
my_image[1000, 1500] = RGB(1.0, 0.0, 0.0)

# â•”â•â•¡ 81b88cbe-64c9-11eb-3b26-39011efb2089
md"""
Be careful: We are actually *modifying* the original image here, even though if we look at the image it is hard to spot, since a single pixel is so small:
"""

# â•”â•â•¡ 73511646-64c9-11eb-24a2-05a55c9c7500
my_image

# â•”â•â•¡ ab9af0f6-64c9-11eb-13d3-5dbdb75a69a7
md"""
## Groups of pixels

We probably want to examine and modify several pixels at once.
For example, we can extract a horizontal strip 1 pixel tall:
"""

# â•”â•â•¡ e29b7954-64cb-11eb-2768-47de07766055
my_image[1000, 1500:1700]

# â•”â•â•¡ 8e7c4866-64cc-11eb-0457-85be566a8966
md"""
Here, Julia is showing the strip as a collection of rectangles in a row.
"""

# â•”â•â•¡ f2ad501a-64cb-11eb-1707-3365d05b300a
md"""
And then modify it:
"""

# â•”â•â•¡ b21eb2f2-64c9-11eb-2cec-d9e61c41cfe2
my_image[1000, 1500:1700] .= RGB(1.0, 0.0, 0.0)

# â•”â•â•¡ 2808339c-64cc-11eb-21d1-c76a9854aa5b
md"""
Similarly we can modify a whole rectangular block of pixels:
"""

# â•”â•â•¡ 33ec0666-64cc-11eb-009f-7502ac8be05f
my_image[1000:1100, 1500:1700] .= RGB(1.0, 0.0, 0.0)

# â•”â•â•¡ 841af282-64cc-11eb-2f98-cb9d43b55ff7
md"""
and look at the result:
"""

# â•”â•â•¡ 3679a622-64cc-11eb-007f-5bb908aec7c3
my_image

# â•”â•â•¡ 693af19c-64cc-11eb-31f3-57ab2fbae597
md"""
## Reducing the size of an image
"""

# â•”â•â•¡ 6361d102-64cc-11eb-31b7-fb631b632040
md"""
Maybe we would also like to reduce the size of this image, since it's rather large. For example, we could take every 10th row and every 10th column and make a new image from the result:
"""

# â•”â•â•¡ ae542fe4-64cc-11eb-29fc-73b7a66314a9
reduced_image = my_image[1:10:end, 1:10:end]

# â•”â•â•¡ c29292b8-64cc-11eb-28db-b52c46e865e6
md"""
Note that the resulting image doesn't look very good, since we seem to have lost too much detail. 

#### Exercise

> Think about what we might do to reduce the size of an image without losing so much detail.
"""

# â•”â•â•¡ 5319c03c-64cc-11eb-0743-a1612476e2d3
md"""
## Saving an image

Finally, we want to be able to save our new creation to a file: 
"""

# â•”â•â•¡ 3db09d92-64cc-11eb-0333-45193c0fd1fe
save("reduced_phil.jpg", reduced_image)

# â•”â•â•¡ dd183eca-6018-11eb-2a83-2fcaeea62942
md"""
# Computer science: Arrays

An image is a concrete example of a fundamental concept in computer science, namely an **array**. 

Just as an image is a rectangular grid, where each grid cell contains a single color,
an array is a rectangular grid for storing data. Data is stored and retrieved using indexing, just as in the image examples: each cell in the grid can store a single "piece of data" of a given type.


## Dimension of an array

An array can be one-dimensional, like the strip of pixels above, two-dimensional, three-dimensional, and so on. The dimension tells us the number of indices that we need to specify a unique location in the grid.
The array object also needs to know the length of the data in each dimension.

## Names for different types of array

One-dimensional arrays are often called **vectors** (or, in some other languages, "lists") and two-dimensional arrays are **matrices**. Higher-dimensional arrays are  **tensors**.


## Arrays as data structures

An array is an example of a **data structure**, i.e. a way of arranging data such that we can access it. A key theme in computer science is that of designing different data structures that represent data in different ways.

Conceptually, we can think of an array as a block of data that has a position or location in space. This can be a useful way to arrange data if, for example, we want to represent the fact that values in nearby locations in array are somehow near to one another.

Images are a good example of this: neighbouring pixels often represent different pieces of the same object, for example the rug or floor, or Philip himself, in the photo. We thus expect neighbouring pixels to be of a similar color. On the other hand, if they are not, this is also useful information, since that may correspond to the edge of an object.

"""

# â•”â•â•¡ 8ddcb286-602a-11eb-3ae0-07d3c77a0f8c
md"""
# Julia: Syntax for creating arrays

## Creating vectors and matrices
Julia has strong support for arrays of any dimension.

Vectors, or one-dimensional arrays, are written using square brackets and commas:
"""

# â•”â•â•¡ 1b2b2b18-64d4-11eb-2d43-e31cb8bc25d1
[RGB(1, 0, 0), RGB(0, 1, 0), RGB(0, 0, 1)]

# â•”â•â•¡ 2b0e6450-64d4-11eb-182b-ff1bd515b56f
md"""
Matrices, or two-dimensional arrays, also use square brackets, but with spaces and new lines instead of commas:
"""

# â•”â•â•¡ 3b2b041a-64d4-11eb-31dd-47d7321ee909
[RGB(1, 0, 0)  RGB(0, 1, 0)
 RGB(0, 0, 1)  RGB(0.5, 0.5, 0.5)]

# â•”â•â•¡ 0f35603a-64d4-11eb-3baf-4fef06d82daa
md"""

## Array comprehensions

It's clear that if we want to create an array with more than a few elements, it will be *very* tedious to do so by hand like this.
Rather, we want to *automate* the process of creating an array by following some pattern, for example to create a whole palette of colors!

Let's start with all the possible colors interpolating between black, `RGB(0, 0, 0)`, and red, `RGB(1, 0, 0)`.  Since only one of the values is changing, we can represent this as a vector, i.e. a one-dimensional array.

A neat method to do this is an **array comprehension**. Again we use square brackets  to create an array, but now we use a **variable** that varies over a given **range** values:
"""

# â•”â•â•¡ e69b02c6-64d6-11eb-02f1-21c4fb5d1043
[RGB(i, 0, 0) for i in 0:0.1:1]

# â•”â•â•¡ fce76132-64d6-11eb-259d-b130038bbae6
md"""
Here, `0:0.1:1` is a **range**; the first and last numbers are the start and end values, and the middle number is the size of the step.
"""

# â•”â•â•¡ 17a69736-64d7-11eb-2c6c-eb5ebf51b285
md"""
In a similar way we can create two-dimensional matrices, by separating the two variables for each dimension with a comma (`,`):
"""

# â•”â•â•¡ 291b04de-64d7-11eb-1ee0-d998dccb998c
[RGB(i, j, 0) for i in 0:0.1:1, j in 0:0.1:1]

# â•”â•â•¡ 5e52d12e-64d7-11eb-0905-c9038a404e24
md"""
# Pluto: Interactivity using sliders
"""

# â•”â•â•¡ 6aba7e62-64d7-11eb-2c49-7944e9e2b94b
md"""
Suppose we want to see the effect of changing the number of colors in our vector or matrix. We could, of course, do so by manually fiddling with the range.

It would be nice if we could do so using a **user interface**, for example with a **slider**. Fortunately, the Pluto notebook allows us to do so!
"""

# â•”â•â•¡ afc66dac-64d7-11eb-1ad0-7f62c20ffefb
md"""
We can define a slider using
"""

# â•”â•â•¡ b37c9868-64d7-11eb-3033-a7b5d3065f7f
@bind number_reds Slider(1:100, show_value=true)

# â•”â•â•¡ b1dfe122-64dc-11eb-1104-1b8852b2c4c5
md"""
[The `Slider` type is defined in the `PlutoUI.jl` package.]
"""

# â•”â•â•¡ cfc55140-64d7-11eb-0ff6-e59c70d01d67
md"""
This creates a new variable called `number_reds`, whose value is the value shown by the slider. When we move the slider, the value of the variable gets updated. Since Pluto is a **reactive** notebook, other cells which use the value of this variable will *automatically be updated too*!
"""

# â•”â•â•¡ fca72490-64d7-11eb-1464-f5e0582c4d18
md"""
Let's use this to make a slider for our one-dimensional collection of reds:
"""

# â•”â•â•¡ 88933746-6028-11eb-32de-13eb6ff43e29
[RGB(red_value / number_reds, 0, 0) for red_value in 0:number_reds]

# â•”â•â•¡ 1c539b02-64d8-11eb-3505-c9288357d139
md"""
When you move the slider, you should see the number of red color patches change!
"""

# â•”â•â•¡ 10f6e6da-64d8-11eb-366f-11f16e73043b
md"""
What is going on here is that we are creating a vector in which `red_value` takes each value in turn from the range from `0` up to the current value of `number_reds`. If we change `number_reds`, then we create a new vector with that new number of red patches.
"""

# â•”â•â•¡ 576d5e3a-64d8-11eb-10c9-876be31f7830
md"""
We can do the same to create different size matrices, by creating two sliders, one for reds and one for greens. Try it out!
"""

# â•”â•â•¡ 82a8314c-64d8-11eb-1acb-e33625381178
md"""
#### Exercise

> Make three sliders with variables `r`, `g` and `b`. Then make a single color patch with the RGB color given by those values.
"""

# â•”â•â•¡ 647fddf2-60ee-11eb-124d-5356c7014c3b
md"""
### Joining matrices

We often want to join vectors and matrices together. We can do so using an extension of the array creation syntax:
"""

# â•”â•â•¡ 7d9ad134-60ee-11eb-1b2a-a7d63f3a7a2d
[reduced_image  reduced_image]

# â•”â•â•¡ 8433b862-60ee-11eb-0cfc-add2b72997dc
[reduced_image                   reverse(reduced_image, dims=2)
 reverse(reduced_image, dims=1)  reverse(reduced_image)]

# â•”â•â•¡ ace86c8a-60ee-11eb-34ef-93c54abc7b1a
md"""
# Summary
"""

# â•”â•â•¡ b08e57e4-60ee-11eb-0e1a-2f49c496668b
md"""
Let's summarize the main ideas from this notebook:
- Images are **arrays** of colors
- We can inspect and modify arrays using **indexing**
- We can create arrays directly or using **array comprehensions**
"""

# â•”â•â•¡ 9025a5b4-6066-11eb-20e8-099e9b8f859e
md"""
----
"""

# â•”â•â•¡ 635a03dd-abd7-49c8-a3d2-e68c7d83cc9b
html"""
<div style="position: relative; right: 0; top: 0; z-index: 300;"><iframe src="https://www.youtube.com/embed/DGojI9xcCfg" width=400 height=250  frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe></div>
"""

# â•”â•â•¡ c8e1b044-891d-45d0-9946-253804470943


# â•”â•â•¡ 57ddff1a-d414-4bd2-8799-5b42a878cb68
md"# Homework: Arrays"

# â•”â•â•¡ 540ccfcc-ee0a-11ea-15dc-4f8120063397
md"""
## **Part 1** - _Manipulating vectors (1D images)_

A `Vector` is a 1D array. We can think of that as a 1D image.

"""

# â•”â•â•¡ 467856dc-eded-11ea-0f83-13d939021ef3
example_vector = [0.5, 0.4, 0.3, 0.2, 0.1, 0.0, 0.7, 0.0, 0.7, 0.9]

# â•”â•â•¡ ad6a33b0-eded-11ea-324c-cfabfd658b56
md"#### Exerise 1.1
ğŸ‘‰ Make a random vector `random_vect` of length 10 using the `rand` function.
"

# â•”â•â•¡ f51333a6-eded-11ea-34e6-bfbb3a69bcb0
random_vect = missing # replace this with your code!

# â•”â•â•¡ cf738088-eded-11ea-2915-61735c2aa990
md"ğŸ‘‰ Make a function `mean` using a `for` loop, which computes the mean/average of a vector of numbers."

# â•”â•â•¡ 0ffa8354-edee-11ea-2883-9d5bfea4a236
function mean(x)
	
	return missing
end

# â•”â•â•¡ 1f104ce4-ee0e-11ea-2029-1d9c817175af
mean([1, 2, 3])

# â•”â•â•¡ 1f229ca4-edee-11ea-2c56-bb00cc6ea53c
md"ğŸ‘‰ Define `m` to be the mean of `random_vect`."

# â•”â•â•¡ 2a391708-edee-11ea-124e-d14698171b68
m = missing

# â•”â•â•¡ e2863d4c-edef-11ea-1d67-332ddca03cc4
md"""ğŸ‘‰ Write a function `demean`, which takes a vector `x` and subtracts the mean from each value in `x`."""

# â•”â•â•¡ ec5efe8c-edef-11ea-2c6f-afaaeb5bc50c
function demean(x)
	
	return missing
end

# â•”â•â•¡ 29e10640-edf0-11ea-0398-17dbf4242de3
md"Let's check that the mean of the `demean(random_vect)` is 0:

_Due to floating-point round-off error it may *not* be *exactly* 0._"

# â•”â•â•¡ 6f67657e-ee1a-11ea-0c2f-3d567bcfa6ea
if ismissing(random_vect)
	md"""
	!!! info
	    The following cells error because `random_vect` is not yet defined. Have you done the first exercise?
	"""
end

# â•”â•â•¡ 73ef1d50-edf0-11ea-343c-d71706874c82
copy_of_random_vect = copy(random_vect); # in case demean modifies `x`

# â•”â•â•¡ 38155b5a-edf0-11ea-3e3f-7163da7433fb
mean(demean(copy_of_random_vect))

# â•”â•â•¡ a5f8bafe-edf0-11ea-0da3-3330861ae43a
md"""
#### Exercise 1.2

ğŸ‘‰ Generate a vector of 100 zeros. Change the center 20 elements to 1.
"""

# â•”â•â•¡ b6b65b94-edf0-11ea-3686-fbff0ff53d08
function create_bar()
	
	return missing
end

# â•”â•â•¡ 22f28dae-edf2-11ea-25b5-11c369ae1253
md"""
#### Exercise 1.3

ğŸ‘‰ Write a function that turns a `Vector` of `Vector`s into a `Matrix`.
"""

# â•”â•â•¡ 8c19fb72-ed6c-11ea-2728-3fa9219eddc4
function vecvec_to_matrix(vecvec)
	
	return missing
end

# â•”â•â•¡ c4761a7e-edf2-11ea-1e75-118e73dadbed
vecvec_to_matrix([[1,2], [3,4]])

# â•”â•â•¡ 393667ca-edf2-11ea-09c5-c5d292d5e896
md"""


ğŸ‘‰ Write a function that turns a `Matrix` into a`Vector` of `Vector`s .
"""

# â•”â•â•¡ 9f1c6d04-ed6c-11ea-007b-75e7e780703d
function matrix_to_vecvec(matrix)
	
	return missing
end

# â•”â•â•¡ 70955aca-ed6e-11ea-2330-89b4d20b1795
matrix_to_vecvec([6 7; 8 9])

# â•”â•â•¡ 5da8cbe8-eded-11ea-2e43-c5b7cc71e133
begin
	colored_line(x::Vector{<:Real}) = Gray.(Float64.((hcat(x)')))
	colored_line(x::Any) = nothing
end

# â•”â•â•¡ 56ced344-eded-11ea-3e81-3936e9ad5777
colored_line(example_vector)

# â•”â•â•¡ b18e2c54-edf1-11ea-0cbf-85946d64b6a2
colored_line(random_vect)

# â•”â•â•¡ d862fb16-edf1-11ea-36ec-615d521e6bc0
colored_line(create_bar())

# â•”â•â•¡ bd540d08-8d17-4bc2-ad0d-1fd65f25fe5d
md"""
This notebook contains _built-in, live answer checks_! In some exercises you will see a coloured box, which runs a test case on your code, and provides feedback based on the result. Simply edit the code, run it, and the check runs again.

_For MIT students:_ there will also be some additional (secret) test cases that will be run as part of the grading process, and we will look at your notebook and write comments.

Feel free to ask questions!
"""

# â•”â•â•¡ 911ccbce-ed68-11ea-3606-0384e7580d7c
# edit the code below to set your name and kerberos ID (i.e. email without @mit.edu)

student = (name = "Jazzy Doe", kerberos_id = "jazz")

# press the â–¶ button in the bottom right of this cell to run your edits
# or use Shift+Enter

# you might need to wait until all other cells in this notebook have completed running. 
# scroll down the page to see what's up

# â•”â•â•¡ 5f95e01a-ee0a-11ea-030c-9dba276aba92
md"_Let's create a package environment:_"

# â•”â•â•¡ e083b3e8-ed61-11ea-2ec9-217820b0a1b4
md"""
## **Part 2** - _Manipulating images_

In this exercise we will get familiar with matrices (2D arrays) in Julia, by manipulating images.
Recall that in Julia images are matrices of `RGB` color objects.

Let's load a picture of Philip again.
"""

# â•”â•â•¡ c5484572-ee05-11ea-0424-f37295c3072d
philip_file = download("https://i.imgur.com/VGPeJ6s.jpg")

# â•”â•â•¡ e86ed944-ee05-11ea-3e0f-d70fc73b789c
md"_Hi there Philip_"

# â•”â•â•¡ c54ccdea-ee05-11ea-0365-23aaf053b7d7
md"""
#### Exercise 2.1
ğŸ‘‰ Write a function **`mean_colors`** that accepts an object called `image`. It should calculate the mean (average) amounts of red, green and blue in the image and return a tuple `(r, g, b)` of those means.
"""

# â•”â•â•¡ f6898df6-ee07-11ea-2838-fde9bc739c11
function mean_colors(image)
	
	return missing
end

# â•”â•â•¡ d75ec078-ee0d-11ea-3723-71fb8eecb040


# â•”â•â•¡ f68d4a36-ee07-11ea-0832-0360530f102e
md"""
#### Exercise 2.2
ğŸ‘‰ Look up the documentation on the `floor` function. Use it to write a function `quantize(x::Number)` that takes in a value $x$ (which you can assume is between 0 and 1) and "quantizes" it into bins of width 0.1. For example, check that 0.267 gets mapped to 0.2.
"""

# â•”â•â•¡ f6991a50-ee07-11ea-0bc4-1d68eb028e6a
begin
	function quantize(x::Number)
		
		return missing
	end
	
	function quantize(color::AbstractRGB)
		# you will write me in a later exercise!
		return missing
	end
	
	function quantize(image::AbstractMatrix)
		# you will write me in a later exercise!
		return missing
	end
end

# â•”â•â•¡ f6a655f8-ee07-11ea-13b6-43ca404ddfc7
quantize(0.267), quantize(0.91)

# â•”â•â•¡ f6b218c0-ee07-11ea-2adb-1968c4fd473a
md"""
#### Exercise 2.3
ğŸ‘‰ Write the second **method** of the function `quantize`, i.e. a new *version* of the function with the *same* name. This method will accept a color object called `color`, of the type `AbstractRGB`. 

_Write the function in the same cell as `quantize(x::Number)` from the last exercise. ğŸ‘†_
    
Here, `::AbstractRGB` is a **type annotation**. This ensures that this version of the function will be chosen when passing in an object whose type is a **subtype** of the `AbstractRGB` abstract type. For example, both the `RGB` and `RGBX` types satisfy this.

The method you write should return a new `RGB` object, in which each component ($r$, $g$ and $b$) are quantized.
"""

# â•”â•â•¡ f6bf64da-ee07-11ea-3efb-05af01b14f67
md"""
#### Exercise 2.4
ğŸ‘‰ Write a method `quantize(image::AbstractMatrix)` that quantizes an image by quantizing each pixel in the image. (You may assume that the matrix is a matrix of color objects.)

_Write the function in the same cell as `quantize(x::Number)` from the last exercise. ğŸ‘†_
"""

# â•”â•â•¡ 25dad7ce-ee0b-11ea-3e20-5f3019dd7fa3
md"Let's apply your method!"

# â•”â•â•¡ f6cc03a0-ee07-11ea-17d8-013991514d42
md"""
#### Exercise 2.5
ğŸ‘‰ Write a function `invert` that inverts a color, i.e. sends $(r, g, b)$ to $(1 - r, 1-g, 1-b)$.
"""

# â•”â•â•¡ 63e8d636-ee0b-11ea-173d-bd3327347d55
function invert(color::AbstractRGB)
	
	return missing
end

# â•”â•â•¡ 2cc2f84e-ee0d-11ea-373b-e7ad3204bb00
md"Let's invert some colors:"

# â•”â•â•¡ b8f26960-ee0a-11ea-05b9-3f4bc1099050
black = RGB(0.0, 0.0, 0.0)

# â•”â•â•¡ 5de3a22e-ee0b-11ea-230f-35df4ca3c96d
invert(black)

# â•”â•â•¡ 4e21e0c4-ee0b-11ea-3d65-b311ae3f98e9
red = RGB(0.8, 0.1, 0.1)

# â•”â•â•¡ 6dbf67ce-ee0b-11ea-3b71-abc05a64dc43
invert(red)

# â•”â•â•¡ 846b1330-ee0b-11ea-3579-7d90fafd7290
md"Can you invert the picture of Philip?"

# â•”â•â•¡ 943103e2-ee0b-11ea-33aa-75a8a1529931
philip_inverted = missing

# â•”â•â•¡ f6d6c71a-ee07-11ea-2b63-d759af80707b
md"""
#### Exercise 2.6
ğŸ‘‰ Write a function `noisify(x::Number, s)` to add randomness of intensity $s$ to a value $x$, i.e. to add a random value between $-s$ and $+s$ to $x$. If the result falls outside the range $(0, 1)$ you should "clamp" it to that range. (Note that Julia has a `clamp` function, but you should write your own function `myclamp(x)`.)
"""

# â•”â•â•¡ f6e2cb2a-ee07-11ea-06ee-1b77e34c1e91
begin
	function noisify(x::Number, s)

		return missing
	end
	
	function noisify(color::AbstractRGB, s)
		# you will write me in a later exercise!
		return missing
	end
	
	function noisify(image::AbstractMatrix, s)
		# you will write me in a later exercise!
		return missing
	end
end

# â•”â•â•¡ f6fc1312-ee07-11ea-39a0-299b67aee3d8
md"""
ğŸ‘‰  Write the second method `noisify(c::AbstractRGB, s)` to add random noise of intensity $s$ to each of the $(r, g, b)$ values in a colour. 

_Write the function in the same cell as `noisify(x::Number)` from the last exercise. ğŸ‘†_
"""

# â•”â•â•¡ 774b4ce6-ee1b-11ea-2b48-e38ee25fc89b
@bind color_noise Slider(0:0.01:1, show_value=true)

# â•”â•â•¡ 7e4aeb70-ee1b-11ea-100f-1952ba66f80f
noisify(red, color_noise)

# â•”â•â•¡ 6a05f568-ee1b-11ea-3b6c-83b6ada3680f


# â•”â•â•¡ f70823d2-ee07-11ea-2bb3-01425212aaf9
md"""
ğŸ‘‰ Write the third method `noisify(image::AbstractMatrix, s)` to noisify each pixel of an image.

_Write the function in the same cell as `noisify(x::Number)` from the last exercise. ğŸ‘†_
"""

# â•”â•â•¡ e70a84d4-ee0c-11ea-0640-bf78653ba102
@bind philip_noise Slider(0:0.01:8, show_value=true)

# â•”â•â•¡ 9604bc44-ee1b-11ea-28f8-7f7af8d0cbb2


# â•”â•â•¡ f714699e-ee07-11ea-08b6-5f5169861b57
md"""
ğŸ‘‰ For which noise intensity does it become unrecognisable? 

You may need noise intensities larger than 1. Why?

"""

# â•”â•â•¡ bdc2df7c-ee0c-11ea-2e9f-7d2c085617c1
answer_about_noise_intensity = md"""
The image is unrecognisable with intensity ...
"""

# â•”â•â•¡ e074560a-601b-11eb-340e-47acd64f03b2
hint(text) = Markdown.MD(Markdown.Admonition("hint", "Hint", [text]))

# â•”â•â•¡ b1d5ca28-edf6-11ea-269e-75a9fb549f1d
hint(md"You can find out more about any function (like `rand`) by creating a new cell and typing:
	
```
?rand
```

Once the Live Docs are open, you can select any code to learn more about it. It might be useful to leave it open all the time, and get documentation while you type code.")

# â•”â•â•¡ f6ef2c2e-ee07-11ea-13a8-2512e7d94426
hint(md"The `rand` function generates (uniform) random floating-point numbers between $0$ and $1$.")

# â•”â•â•¡ e0776548-601b-11eb-2563-57ba2cf1d5d1
almost(text) = Markdown.MD(Markdown.Admonition("warning", "Almost there!", [text]))

# â•”â•â•¡ e083bef6-601b-11eb-2134-e3063d5c4253
still_missing(text=md"Replace `missing` with your answer.") = Markdown.MD(Markdown.Admonition("warning", "Here we go!", [text]))

# â•”â•â•¡ e08ecb84-601b-11eb-0e25-152ed3a262f7
keep_working(text=md"The answer is not quite right.") = Markdown.MD(Markdown.Admonition("danger", "Keep working on it!", [text]))

# â•”â•â•¡ e09036a4-601b-11eb-1a8b-ef70105ab91c
yays = [md"Great!", md"Yay â¤", md"Great! ğŸ‰", md"Well done!", md"Keep it up!", md"Good job!", md"Awesome!", md"You got the right answer!", md"Let's move on to the next section."]

# â•”â•â•¡ e09af1a2-601b-11eb-14c8-57a46546f6ce
correct(text=rand(yays)) = Markdown.MD(Markdown.Admonition("correct", "Got it!", [text]))

# â•”â•â•¡ e0a4fc10-601b-11eb-211d-03570aca2726
not_defined(variable_name) = Markdown.MD(Markdown.Admonition("danger", "Oopsie!", [md"Make sure that you define a variable called **$(Markdown.Code(string(variable_name)))**"]))

# â•”â•â•¡ 397941fc-edee-11ea-33f2-5d46c759fbf7
if !@isdefined(random_vect)
	not_defined(:random_vect)
elseif ismissing(random_vect)
	still_missing()
elseif !(random_vect isa Vector)
	keep_working(md"`random_vect` should be a `Vector`.")
elseif length(random_vect) != 10
	keep_working(md"`random_vect` does not have the correct size.")
else
	correct()
end

# â•”â•â•¡ 38dc80a0-edef-11ea-10e9-615255a4588c
if !@isdefined(mean)
	not_defined(:mean)
else
	let
		result = mean([1,2,3])
		if ismissing(result)
			still_missing()
		elseif isnothing(result)
			keep_working(md"Did you forget to write `return`?")
		elseif result != 2
			keep_working()
		else
			correct()
		end
	end
end

# â•”â•â•¡ 2b1ccaca-edee-11ea-34b0-c51659f844d0
if !@isdefined(m)
	not_defined(:m)
elseif ismissing(m)
	still_missing()
elseif !(m isa Number)
	keep_working(md"`m` should be a number.")
elseif m != mean(random_vect)
	keep_working()
else
	correct()
end

# â•”â•â•¡ e3394c8a-edf0-11ea-1bb8-619f7abb6881
if !@isdefined(create_bar)
	not_defined(:create_bar)
else
	let
		result = create_bar()
		if ismissing(result)
			still_missing()
		elseif isnothing(result)
			keep_working(md"Did you forget to write `return`?")
		elseif !(result isa Vector) || length(result) != 100
			keep_working(md"The result should be a `Vector` with 100 elements.")
		elseif result[[1,50,100]] != [0,1,0]
			keep_working()
		else
			correct()
		end
	end
end

# â•”â•â•¡ adfbe9b2-ed6c-11ea-09ac-675262f420df
if !@isdefined(vecvec_to_matrix)
	not_defined(:vecvec_to_matrix)
else
	let
		input = [[6,7],[8,9]]

		result = vecvec_to_matrix(input)
		shouldbe = [6 7; 8 9]

		if ismissing(result)
			still_missing()
		elseif isnothing(result)
			keep_working(md"Did you forget to write `return`?")
		elseif !(result isa Matrix)
			keep_working(md"The result should be a `Matrix`")
		elseif result != shouldbe && result != shouldbe'
			keep_working()
		else
			correct()
		end
	end
end

# â•”â•â•¡ e06b7fbc-edf2-11ea-1708-fb32599dded3
if !@isdefined(matrix_to_vecvec)
	not_defined(:matrix_to_vecvec)
else
	let
		input = [6 7 8; 8 9 10]
		result = matrix_to_vecvec(input)
		shouldbe = [[6,7,8],[8,9,10]]
		shouldbe2 = [[6,8], [7,9], [8,10]]

		if ismissing(result)
			still_missing()
		elseif isnothing(result)
			keep_working(md"Did you forget to write `return`?")
		elseif result != shouldbe && result != shouldbe2
			keep_working()
		else
			correct()
		end
	end
end

# â•”â•â•¡ 4d0158d0-ee0d-11ea-17c3-c169d4284acb
if !@isdefined(mean_colors)
	not_defined(:mean_colors)
else
	let
		input = reshape([RGB(1.0, 1.0, 1.0), RGB(1.0, 1.0, 0.0)], (2,1))
		
		result = mean_colors(input)
		shouldbe = (1.0, 1.0, 0.5)
		shouldbe2 = RGB(shouldbe...)

		if ismissing(result)
			still_missing()
		elseif isnothing(result)
			keep_working(md"Did you forget to write `return`?")
		elseif !(result == shouldbe) && !(result == shouldbe2)
			keep_working()
		else
			correct()
		end
	end
end

# â•”â•â•¡ c905b73e-ee1a-11ea-2e36-23b8e73bfdb6
if !@isdefined(quantize)
	not_defined(:quantize)
else
	let
		result = quantize(.3)

		if ismissing(result)
			still_missing()
		elseif isnothing(result)
			keep_working(md"Did you forget to write `return`?")
		elseif result != .3
			if quantize(0.35) == .3
				almost(md"What should quantize(`0.2`) be?")
			else
				keep_working()
			end
		else
			correct()
		end
	end
end

# â•”â•â•¡ e0a6031c-601b-11eb-27a5-65140dd92897
bigbreak = html"<br><br><br><br><br>";

# â•”â•â•¡ 45815734-ee0a-11ea-2982-595e1fc0e7b1
bigbreak

# â•”â•â•¡ 54056a02-ee0a-11ea-101f-47feb6623bec
bigbreak

# â•”â•â•¡ e0b15582-601b-11eb-26d6-bbf708933bc8
function camera_input(;max_size=200, default_url="https://i.imgur.com/SUmi94P.png")
"""
<span class="pl-image waiting-for-permission">
<style>
	
	.pl-image.popped-out {
		position: fixed;
		top: 0;
		right: 0;
		z-index: 5;
	}

	.pl-image #video-container {
		width: 250px;
	}

	.pl-image video {
		border-radius: 1rem 1rem 0 0;
	}
	.pl-image.waiting-for-permission #video-container {
		display: none;
	}
	.pl-image #prompt {
		display: none;
	}
	.pl-image.waiting-for-permission #prompt {
		width: 250px;
		height: 200px;
		display: grid;
		place-items: center;
		font-family: monospace;
		font-weight: bold;
		text-decoration: underline;
		cursor: pointer;
		border: 5px dashed rgba(0,0,0,.5);
	}

	.pl-image video {
		display: block;
	}
	.pl-image .bar {
		width: inherit;
		display: flex;
		z-index: 6;
	}
	.pl-image .bar#top {
		position: absolute;
		flex-direction: column;
	}
	
	.pl-image .bar#bottom {
		background: black;
		border-radius: 0 0 1rem 1rem;
	}
	.pl-image .bar button {
		flex: 0 0 auto;
		background: rgba(255,255,255,.8);
		border: none;
		width: 2rem;
		height: 2rem;
		border-radius: 100%;
		cursor: pointer;
		z-index: 7;
	}
	.pl-image .bar button#shutter {
		width: 3rem;
		height: 3rem;
		margin: -1.5rem auto .2rem auto;
	}

	.pl-image video.takepicture {
		animation: pictureflash 200ms linear;
	}

	@keyframes pictureflash {
		0% {
			filter: grayscale(1.0) contrast(2.0);
		}

		100% {
			filter: grayscale(0.0) contrast(1.0);
		}
	}
</style>

	<div id="video-container">
		<div id="top" class="bar">
			<button id="stop" title="Stop video">âœ–</button>
			<button id="pop-out" title="Pop out/pop in">â</button>
		</div>
		<video playsinline autoplay></video>
		<div id="bottom" class="bar">
		<button id="shutter" title="Click to take a picture">ğŸ“·</button>
		</div>
	</div>
		
	<div id="prompt">
		<span>
		Enable webcam
		</span>
	</div>

<script>
	// based on https://github.com/fonsp/printi-static (by the same author)

	const span = currentScript.parentElement
	const video = span.querySelector("video")
	const popout = span.querySelector("button#pop-out")
	const stop = span.querySelector("button#stop")
	const shutter = span.querySelector("button#shutter")
	const prompt = span.querySelector(".pl-image #prompt")

	const maxsize = $(max_size)

	const send_source = (source, src_width, src_height) => {
		const scale = Math.min(1.0, maxsize / src_width, maxsize / src_height)

		const width = Math.floor(src_width * scale)
		const height = Math.floor(src_height * scale)

		const canvas = html`<canvas width=\${width} height=\${height}>`
		const ctx = canvas.getContext("2d")
		ctx.drawImage(source, 0, 0, width, height)

		span.value = {
			width: width,
			height: height,
			data: ctx.getImageData(0, 0, width, height).data,
		}
		span.dispatchEvent(new CustomEvent("input"))
	}
	
	const clear_camera = () => {
		window.stream.getTracks().forEach(s => s.stop());
		video.srcObject = null;

		span.classList.add("waiting-for-permission");
	}

	prompt.onclick = () => {
		navigator.mediaDevices.getUserMedia({
			audio: false,
			video: {
				facingMode: "environment",
			},
		}).then(function(stream) {

			stream.onend = console.log

			window.stream = stream
			video.srcObject = stream
			window.cameraConnected = true
			video.controls = false
			video.play()
			video.controls = false

			span.classList.remove("waiting-for-permission");

		}).catch(function(error) {
			console.log(error)
		});
	}
	stop.onclick = () => {
		clear_camera()
	}
	popout.onclick = () => {
		span.classList.toggle("popped-out")
	}

	shutter.onclick = () => {
		const cl = video.classList
		cl.remove("takepicture")
		void video.offsetHeight
		cl.add("takepicture")
		video.play()
		video.controls = false
		console.log(video)
		send_source(video, video.videoWidth, video.videoHeight)
	}
	
	
	document.addEventListener("visibilitychange", () => {
		if (document.visibilityState != "visible") {
			clear_camera()
		}
	})


	// Set a default image

	const img = html`<img crossOrigin="anonymous">`

	img.onload = () => {
	console.log("helloo")
		send_source(img, img.width, img.height)
	}
	img.src = "$(default_url)"
	console.log(img)
</script>
</span>
""" |> HTML
end

# â•”â•â•¡ e891fce0-601b-11eb-383b-bde5b128822e
function process_raw_camera_data(raw_camera_data)
	# the raw image data is a long byte array, we need to transform it into something
	# more "Julian" - something with more _structure_.
	
	# The encoding of the raw byte stream is:
	# every 4 bytes is a single pixel
	# every pixel has 4 values: Red, Green, Blue, Alpha
	# (we ignore alpha for this notebook)
	
	# So to get the red values for each pixel, we take every 4th value, starting at 
	# the 1st:
	reds_flat = UInt8.(raw_camera_data["data"][1:4:end])
	greens_flat = UInt8.(raw_camera_data["data"][2:4:end])
	blues_flat = UInt8.(raw_camera_data["data"][3:4:end])
	
	# but these are still 1-dimensional arrays, nicknamed 'flat' arrays
	# We will 'reshape' this into 2D arrays:
	
	width = raw_camera_data["width"]
	height = raw_camera_data["height"]
	
	# shuffle and flip to get it in the right shape
	reds = reshape(reds_flat, (width, height))' / 255.0
	greens = reshape(greens_flat, (width, height))' / 255.0
	blues = reshape(blues_flat, (width, height))' / 255.0
	
	# we have our 2D array for each color
	# Let's create a single 2D array, where each value contains the R, G and B value of 
	# that pixel
	
	RGB.(reds, greens, blues)
end

# â•”â•â•¡ 81510a30-ee0e-11ea-0062-8b3327428f9d


# â•”â•â•¡ 6b30dc38-ed6b-11ea-10f3-ab3f121bf4b8
# begin
# 	Pkg.add("PlutoUI")
# 	using PlutoUI
# end

# â•”â•â•¡ edf900be-601b-11eb-0456-3f7cfc5e876b
md"_Lecture 1, Spring 2021, version 0_"

# â•”â•â•¡ e3b03628-ee05-11ea-23b6-27c7b0210532
decimate(image, ratio=5) = image[1:ratio:end, 1:ratio:end]

# â•”â•â•¡ c8ecfe5c-ee05-11ea-322b-4b2714898831
philip = let
	original = Images.load(philip_file)
	decimate(original, 8)
end

# â•”â•â•¡ 5be9b144-ee0d-11ea-2a8d-8775de265a1d
mean_colors(philip)

# â•”â•â•¡ 9751586e-ee0c-11ea-0cbb-b7eda92977c9
quantize(philip)

# â•”â•â•¡ ac15e0d0-ee0c-11ea-1eaf-d7f88b5df1d7
noisify(philip, philip_noise)

# â•”â•â•¡ ef56f47a-601b-11eb-0c3f-7904034396f1
md"""

Submission by: **_$(student.name)_** ($(student.kerberos_id)@mit.edu)
"""

# â•”â•â•¡ Cell order:
# â•Ÿâ”€e91d7926-ec6e-41e7-aba2-9dca333c8aa5
# â• â•d07fcdb0-7afc-4a25-b68a-49fd1e3405e7
# â•Ÿâ”€ca1b507e-6017-11eb-34e6-6b85cd189002
# â•Ÿâ”€127daf08-601b-11eb-28c1-2139c8d1a65a
# â•Ÿâ”€9eb6efd2-6018-11eb-2db8-c3ce41d9e337
# â•Ÿâ”€e37e4d40-6018-11eb-3e1d-093266c98507
# â•Ÿâ”€e1c9742a-6018-11eb-23ba-d974e57f78f9
# â•Ÿâ”€62fa19da-64c6-11eb-0038-5d40a6890cf5
# â• â•34ee0954-601e-11eb-1912-97dc2937fd52
# â• â•9180fbcc-601e-11eb-0c22-c920dc7ee9a9
# â• â•34ffc3d8-601e-11eb-161c-6f9a07c5fd78
# â•Ÿâ”€abaaa980-601e-11eb-0f71-8ff02269b775
# â• â•aafe76a6-601e-11eb-1ff5-01885c5238da
# â•Ÿâ”€c99d2aa8-601e-11eb-3469-497a246db17c
# â•Ÿâ”€cef1a95a-64c6-11eb-15e7-636a3621d727
# â•Ÿâ”€f26d9326-64c6-11eb-1166-5d82586422ed
# â•Ÿâ”€6f928b30-602c-11eb-1033-71d442feff93
# â• â•75c5c85a-602c-11eb-2fb1-f7e7f2c5d04b
# â•Ÿâ”€77f93eb8-602c-11eb-1f38-efa56cc93ca5
# â•Ÿâ”€f9244264-64c6-11eb-23a6-cfa76f8aff6d
# â• â•bd22d09a-64c7-11eb-146f-67733b8be241
# â•Ÿâ”€28860d48-64c8-11eb-240f-e1232b3638df
# â•Ÿâ”€622f514e-64c8-11eb-0c67-c7940e894973
# â•Ÿâ”€4504577c-64c8-11eb-343b-3369b6d10d8b
# â•Ÿâ”€40886d36-64c9-11eb-3c69-4b68673a6dde
# â• â•552235ec-64c9-11eb-1f7f-f76da2818cb3
# â•Ÿâ”€5a0cc342-64c9-11eb-1211-f1b06d652497
# â•Ÿâ”€2ee543b2-64d6-11eb-3c39-c5660141787e
# â• â•62da7c94-64c9-11eb-10dd-a374ce105d81
# â•Ÿâ”€81b88cbe-64c9-11eb-3b26-39011efb2089
# â• â•73511646-64c9-11eb-24a2-05a55c9c7500
# â•Ÿâ”€ab9af0f6-64c9-11eb-13d3-5dbdb75a69a7
# â• â•e29b7954-64cb-11eb-2768-47de07766055
# â•Ÿâ”€8e7c4866-64cc-11eb-0457-85be566a8966
# â•Ÿâ”€f2ad501a-64cb-11eb-1707-3365d05b300a
# â• â•b21eb2f2-64c9-11eb-2cec-d9e61c41cfe2
# â•Ÿâ”€2808339c-64cc-11eb-21d1-c76a9854aa5b
# â• â•33ec0666-64cc-11eb-009f-7502ac8be05f
# â•Ÿâ”€841af282-64cc-11eb-2f98-cb9d43b55ff7
# â• â•3679a622-64cc-11eb-007f-5bb908aec7c3
# â•Ÿâ”€693af19c-64cc-11eb-31f3-57ab2fbae597
# â•Ÿâ”€6361d102-64cc-11eb-31b7-fb631b632040
# â• â•ae542fe4-64cc-11eb-29fc-73b7a66314a9
# â•Ÿâ”€c29292b8-64cc-11eb-28db-b52c46e865e6
# â•Ÿâ”€5319c03c-64cc-11eb-0743-a1612476e2d3
# â• â•3db09d92-64cc-11eb-0333-45193c0fd1fe
# â•Ÿâ”€dd183eca-6018-11eb-2a83-2fcaeea62942
# â•Ÿâ”€8ddcb286-602a-11eb-3ae0-07d3c77a0f8c
# â• â•1b2b2b18-64d4-11eb-2d43-e31cb8bc25d1
# â•Ÿâ”€2b0e6450-64d4-11eb-182b-ff1bd515b56f
# â• â•3b2b041a-64d4-11eb-31dd-47d7321ee909
# â•Ÿâ”€0f35603a-64d4-11eb-3baf-4fef06d82daa
# â• â•e69b02c6-64d6-11eb-02f1-21c4fb5d1043
# â•Ÿâ”€fce76132-64d6-11eb-259d-b130038bbae6
# â•Ÿâ”€17a69736-64d7-11eb-2c6c-eb5ebf51b285
# â• â•291b04de-64d7-11eb-1ee0-d998dccb998c
# â•Ÿâ”€5e52d12e-64d7-11eb-0905-c9038a404e24
# â•Ÿâ”€6aba7e62-64d7-11eb-2c49-7944e9e2b94b
# â•Ÿâ”€afc66dac-64d7-11eb-1ad0-7f62c20ffefb
# â• â•b37c9868-64d7-11eb-3033-a7b5d3065f7f
# â•Ÿâ”€b1dfe122-64dc-11eb-1104-1b8852b2c4c5
# â•Ÿâ”€cfc55140-64d7-11eb-0ff6-e59c70d01d67
# â•Ÿâ”€fca72490-64d7-11eb-1464-f5e0582c4d18
# â• â•88933746-6028-11eb-32de-13eb6ff43e29
# â•Ÿâ”€1c539b02-64d8-11eb-3505-c9288357d139
# â•Ÿâ”€10f6e6da-64d8-11eb-366f-11f16e73043b
# â•Ÿâ”€576d5e3a-64d8-11eb-10c9-876be31f7830
# â•Ÿâ”€82a8314c-64d8-11eb-1acb-e33625381178
# â•Ÿâ”€647fddf2-60ee-11eb-124d-5356c7014c3b
# â• â•7d9ad134-60ee-11eb-1b2a-a7d63f3a7a2d
# â• â•8433b862-60ee-11eb-0cfc-add2b72997dc
# â•Ÿâ”€ace86c8a-60ee-11eb-34ef-93c54abc7b1a
# â•Ÿâ”€b08e57e4-60ee-11eb-0e1a-2f49c496668b
# â•Ÿâ”€9025a5b4-6066-11eb-20e8-099e9b8f859e
# â•Ÿâ”€635a03dd-abd7-49c8-a3d2-e68c7d83cc9b
# â• â•c8e1b044-891d-45d0-9946-253804470943
# â•Ÿâ”€57ddff1a-d414-4bd2-8799-5b42a878cb68
# â•Ÿâ”€540ccfcc-ee0a-11ea-15dc-4f8120063397
# â•Ÿâ”€467856dc-eded-11ea-0f83-13d939021ef3
# â• â•56ced344-eded-11ea-3e81-3936e9ad5777
# â•Ÿâ”€ad6a33b0-eded-11ea-324c-cfabfd658b56
# â• â•f51333a6-eded-11ea-34e6-bfbb3a69bcb0
# â•Ÿâ”€b18e2c54-edf1-11ea-0cbf-85946d64b6a2
# â•Ÿâ”€397941fc-edee-11ea-33f2-5d46c759fbf7
# â•Ÿâ”€b1d5ca28-edf6-11ea-269e-75a9fb549f1d
# â•Ÿâ”€cf738088-eded-11ea-2915-61735c2aa990
# â• â•0ffa8354-edee-11ea-2883-9d5bfea4a236
# â• â•1f104ce4-ee0e-11ea-2029-1d9c817175af
# â•Ÿâ”€38dc80a0-edef-11ea-10e9-615255a4588c
# â•Ÿâ”€1f229ca4-edee-11ea-2c56-bb00cc6ea53c
# â• â•2a391708-edee-11ea-124e-d14698171b68
# â•Ÿâ”€2b1ccaca-edee-11ea-34b0-c51659f844d0
# â•Ÿâ”€e2863d4c-edef-11ea-1d67-332ddca03cc4
# â• â•ec5efe8c-edef-11ea-2c6f-afaaeb5bc50c
# â•Ÿâ”€29e10640-edf0-11ea-0398-17dbf4242de3
# â•Ÿâ”€6f67657e-ee1a-11ea-0c2f-3d567bcfa6ea
# â• â•38155b5a-edf0-11ea-3e3f-7163da7433fb
# â• â•73ef1d50-edf0-11ea-343c-d71706874c82
# â•Ÿâ”€a5f8bafe-edf0-11ea-0da3-3330861ae43a
# â• â•b6b65b94-edf0-11ea-3686-fbff0ff53d08
# â•Ÿâ”€d862fb16-edf1-11ea-36ec-615d521e6bc0
# â•Ÿâ”€e3394c8a-edf0-11ea-1bb8-619f7abb6881
# â•Ÿâ”€22f28dae-edf2-11ea-25b5-11c369ae1253
# â• â•8c19fb72-ed6c-11ea-2728-3fa9219eddc4
# â• â•c4761a7e-edf2-11ea-1e75-118e73dadbed
# â•Ÿâ”€adfbe9b2-ed6c-11ea-09ac-675262f420df
# â•Ÿâ”€393667ca-edf2-11ea-09c5-c5d292d5e896
# â• â•9f1c6d04-ed6c-11ea-007b-75e7e780703d
# â• â•70955aca-ed6e-11ea-2330-89b4d20b1795
# â•Ÿâ”€e06b7fbc-edf2-11ea-1708-fb32599dded3
# â•Ÿâ”€5da8cbe8-eded-11ea-2e43-c5b7cc71e133
# â•Ÿâ”€45815734-ee0a-11ea-2982-595e1fc0e7b1
# â•Ÿâ”€bd540d08-8d17-4bc2-ad0d-1fd65f25fe5d
# â• â•911ccbce-ed68-11ea-3606-0384e7580d7c
# â•Ÿâ”€5f95e01a-ee0a-11ea-030c-9dba276aba92
# â• â•74b008f6-ed6b-11ea-291f-b3791d6d1b35
# â•Ÿâ”€54056a02-ee0a-11ea-101f-47feb6623bec
# â•Ÿâ”€e083b3e8-ed61-11ea-2ec9-217820b0a1b4
# â• â•c5484572-ee05-11ea-0424-f37295c3072d
# â• â•c8ecfe5c-ee05-11ea-322b-4b2714898831
# â•Ÿâ”€e86ed944-ee05-11ea-3e0f-d70fc73b789c
# â•Ÿâ”€c54ccdea-ee05-11ea-0365-23aaf053b7d7
# â• â•f6898df6-ee07-11ea-2838-fde9bc739c11
# â• â•5be9b144-ee0d-11ea-2a8d-8775de265a1d
# â•Ÿâ”€4d0158d0-ee0d-11ea-17c3-c169d4284acb
# â• â•d75ec078-ee0d-11ea-3723-71fb8eecb040
# â•Ÿâ”€f68d4a36-ee07-11ea-0832-0360530f102e
# â• â•f6991a50-ee07-11ea-0bc4-1d68eb028e6a
# â• â•f6a655f8-ee07-11ea-13b6-43ca404ddfc7
# â•Ÿâ”€c905b73e-ee1a-11ea-2e36-23b8e73bfdb6
# â•Ÿâ”€f6b218c0-ee07-11ea-2adb-1968c4fd473a
# â•Ÿâ”€f6bf64da-ee07-11ea-3efb-05af01b14f67
# â•Ÿâ”€25dad7ce-ee0b-11ea-3e20-5f3019dd7fa3
# â• â•9751586e-ee0c-11ea-0cbb-b7eda92977c9
# â•Ÿâ”€f6cc03a0-ee07-11ea-17d8-013991514d42
# â• â•63e8d636-ee0b-11ea-173d-bd3327347d55
# â•Ÿâ”€2cc2f84e-ee0d-11ea-373b-e7ad3204bb00
# â•Ÿâ”€b8f26960-ee0a-11ea-05b9-3f4bc1099050
# â• â•5de3a22e-ee0b-11ea-230f-35df4ca3c96d
# â• â•4e21e0c4-ee0b-11ea-3d65-b311ae3f98e9
# â• â•6dbf67ce-ee0b-11ea-3b71-abc05a64dc43
# â•Ÿâ”€846b1330-ee0b-11ea-3579-7d90fafd7290
# â• â•943103e2-ee0b-11ea-33aa-75a8a1529931
# â•Ÿâ”€f6d6c71a-ee07-11ea-2b63-d759af80707b
# â• â•f6e2cb2a-ee07-11ea-06ee-1b77e34c1e91
# â•Ÿâ”€f6ef2c2e-ee07-11ea-13a8-2512e7d94426
# â•Ÿâ”€f6fc1312-ee07-11ea-39a0-299b67aee3d8
# â•Ÿâ”€774b4ce6-ee1b-11ea-2b48-e38ee25fc89b
# â• â•7e4aeb70-ee1b-11ea-100f-1952ba66f80f
# â•Ÿâ”€6a05f568-ee1b-11ea-3b6c-83b6ada3680f
# â•Ÿâ”€f70823d2-ee07-11ea-2bb3-01425212aaf9
# â• â•e70a84d4-ee0c-11ea-0640-bf78653ba102
# â• â•ac15e0d0-ee0c-11ea-1eaf-d7f88b5df1d7
# â•Ÿâ”€9604bc44-ee1b-11ea-28f8-7f7af8d0cbb2
# â•Ÿâ”€f714699e-ee07-11ea-08b6-5f5169861b57
# â• â•bdc2df7c-ee0c-11ea-2e9f-7d2c085617c1
# â•Ÿâ”€e074560a-601b-11eb-340e-47acd64f03b2
# â•Ÿâ”€e0776548-601b-11eb-2563-57ba2cf1d5d1
# â•Ÿâ”€e083bef6-601b-11eb-2134-e3063d5c4253
# â•Ÿâ”€e08ecb84-601b-11eb-0e25-152ed3a262f7
# â•Ÿâ”€e09036a4-601b-11eb-1a8b-ef70105ab91c
# â•Ÿâ”€e09af1a2-601b-11eb-14c8-57a46546f6ce
# â•Ÿâ”€e0a4fc10-601b-11eb-211d-03570aca2726
# â• â•e0a6031c-601b-11eb-27a5-65140dd92897
# â•Ÿâ”€e0b15582-601b-11eb-26d6-bbf708933bc8
# â•Ÿâ”€e891fce0-601b-11eb-383b-bde5b128822e
# â•Ÿâ”€81510a30-ee0e-11ea-0062-8b3327428f9d
# â• â•6b30dc38-ed6b-11ea-10f3-ab3f121bf4b8
# â•Ÿâ”€edf900be-601b-11eb-0456-3f7cfc5e876b
# â• â•e3b03628-ee05-11ea-23b6-27c7b0210532
# â•Ÿâ”€ef56f47a-601b-11eb-0c3f-7904034396f1
